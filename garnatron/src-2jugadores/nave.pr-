
//-----------------------------------------------------------------------
// Maneja la nave protagonista
//-----------------------------------------------------------------------

PROCESS nave01(x,y,jugador)

private acelx; acely; disparando; inmunidad=75; retardo=5; gatillo;

BEGIN
	graph=1;
	file=fpg_nave;
	controlador(jugador);
	loop
		LOOP
			if(pausa==0)
				if(inmunidad<75) inmunidad++; estado=0; flags=4; else flags=0; end
				if(retardo<5) retardo++; end
				if(estado==-1 and inmunidad==75) 
					if(p[jugador].escudo>0) 
						p[jugador].escudo--; 
						inmunidad=0; 
					else 
						explosion(x,y,3,200);
						explotalo(x,y,z,alpha,angle,file,graph,20);
						break; 
					end 
				end
				if(p[jugador].botones[1] and acelx<10) graph=3; acelx+=2; end
				if(p[jugador].botones[0] and acelx>-10) graph=2; acelx-=2; end
				if(p[jugador].botones[2] and acely>-10) acely-=2; end
				if(p[jugador].botones[3] and acely<10) acely+=2; end

				if(mouse.left)
					if(mouse.x-x<-12 and acelx>-10) graph=2; acelx-=3; end 
					if(mouse.x-x>12 and acelx<10) graph=3; acelx+=3; end
					if(mouse.y-y<-12 and acely>-10) acely-=3; end
					if(mouse.y-y>12 and acely<10) acely+=3; end
				end
				
				if(x>740) x=740; end
				if(x<60) x=60; end
				if(y<140) y=140; end
				if(y>540) y=540; end

				if(acelx>0 and not p[jugador].botones[1] and x<730) acelx--; end
				if(acelx<0 and not p[jugador].botones[0] and x>70) acelx++; end
				if(acely<0 and not p[jugador].botones[2] and y>150) acely++; end
				if(acely>0 and not p[jugador].botones[3] and y<530) acely--; end

				x+=acelx; y+=acely;
				if(acelx==0) graph=1; end
				
				if(p[jugador].botones[6] and gatillo==0) p[jugador].habil++; gatillo=1; end
				if(not p[jugador].botones[6] and gatillo==1) gatillo=0; end
				
				IF (p[jugador].botones[4] and disparando==0 and retardo==5)
					retardo=0;
					if(p[jugador].poder==1)
						disparo01(x-15,y-10,angle+pi/2);
						disparo01(x+15,y-10,angle+pi/2);
							suena(s_disparo);
					END
					if(p[jugador].poder==2)
						disparo01(x,y-15,angle+pi/2);
						disparo01(x-20,y+10,angle+pi/2);
						disparo01(x+20,y+10,angle+pi/2);
						suena(s_disparo);
					END
					if(p[jugador].poder==3)
						disparo01(x-30,y+15,angle+pi/2);
						disparo01(x-10,y-10,angle+pi/2);
						disparo01(x+10,y-10,angle+pi/2);
						disparo01(x+30,y+15,angle+pi/2);
						suena(s_disparo);
					END
					if(p[jugador].poder==4)
						disparo01(x-30,y+15,angle+pi*2/3);
						disparo01(x-30,y+15,angle+pi/2);
						disparo01(x-10,y-10,angle+pi/2);
						disparo01(x+10,y-10,angle+pi/2);
						disparo01(x+30,y+15,angle+pi/2);
						disparo01(x+30,y+15,angle+pi*1/3);
						suena(s_disparo);
					END
				end
				if(disparando==0 and p[jugador].botones[5])
					if(p[jugador].habil==1)
						if(p[jugador].energia>4)
							bomba(p[jugador].habil); p[jugador].energia-=5; disparando=1;
						end
					end
					if(p[jugador].habil==2)
						if(p[jugador].energia>9)
							bomba(p[jugador].habil); p[jugador].energia-=10; disparando=1;
						end
					end
					if(p[jugador].habil==3)
						if(p[jugador].energia>9)
							bomba(p[jugador].habil); p[jugador].energia-=10; disparando=1;
						end
					end
					if(p[jugador].habil==4)
						if(p[jugador].energia>1)
							bomba(p[jugador].habil); p[jugador].energia-=1; disparando=1;
						end
					end
					if(p[jugador].habil==5)
						if(p[jugador].energia>6)
							bomba(p[jugador].habil); p[jugador].energia-=7; disparando=1;
						end
					end
				end
				if(not exists(type bomba))
					disparando=0;
				end
			end
			FRAME;
		END
		if(p[jugador].poder>1) p[jugador].poder--; end
		p[jugador].escudo=5;
		inmunidad=0;
		estado=0;
		if(p[jugador].vidas>0) p[jugador].vidas--; else break; end
		x=400;
		y=650;
		while(y>550) y-=5; frame; end
		frame;
	end
END

//-----------------------------------------------------------------------
// Maneja marcadores
//-----------------------------------------------------------------------

process marcador();

private direccion=1; time;

begin
x=400; y=50; z=-100; file=fpg_menu;

graph=20;

barra_energia();
barra_escudo();
habilidad();
objeto(100,50,12,fpg_nave,100,0);
objeto(300,50,1,fpg_nave,20,0);
write_var(fuente1,320,50,4,p[1].vidas);

loop
	if(pausa==0)

		time++;
		if(time>125 and p[1].energia<20) p[1].energia++; time=0; end

		if(direccion==1) graph++; else graph--; end
		if(graph==20) direccion=1; end
		if(graph==44) direccion=0; end

		if(p[1].habil>5) p[1].habil=1; end
		if(p[1].habil<1) p[1].habil=5; end
		if(p[1].escudo>5) p[1].escudo=5; end
		if(p[1].poder>4) p[1].poder=4; end
		if(p[1].energia>20) p[1].energia=20; end
	end
	frame;
end
end

//-----------------------------------------------------------------------
// muestra la habilidad
//-----------------------------------------------------------------------

process habilidad();

begin
x=400; y=50; z=-110; file=fpg_nave;
loop
	if(p[1].habil==1) graph=30; end
	if(p[1].habil==2) graph=31; end
	if(p[1].habil==3) graph=32; end
	if(p[1].habil==4) graph=33; end
	if(p[1].habil==5) graph=34; end
	if(p[1].habil==6) graph=0; end
 	frame;
end
end

//-----------------------------------------------------------------------
// Maneja la barra de energia
//-----------------------------------------------------------------------
process barra_energia();

private
energia2;

begin
x=100; y=50; z=-110; graph=11; region=2; file=fpg_nave;
loop
	if(pausa==0)
		if(p[1].energia>energia2) energia2++; end
		if(p[1].energia<energia2) energia2--; end
		define_region(2,25,30,energia2*150/20,60);
	end
	frame;
end
end

//-----------------------------------------------------------------------
// Maneja la barra de escudo
//-----------------------------------------------------------------------

process barra_escudo();

private
escudo2;
escudo_ant;

begin
x=700; y=50; z=-110; graph=10; region=3; file=fpg_nave;
loop
	if(pausa==0)

		if(escudo_ant>p[1].escudo)
			if(p[1].escudo==4) explotalo(635,y,z,alpha,angle,file,20,10); end
			if(p[1].escudo==3) explotalo(670,y,z,alpha,angle,file,20,10); end
			if(p[1].escudo==2) explotalo(705,y,z,alpha,angle,file,20,10); end
			if(p[1].escudo==1) explotalo(735,y,z,alpha,angle,file,20,10); end
			if(p[1].escudo==0) explotalo(765,y,z,alpha,angle,file,20,10); end		
		end

		if(p[1].escudo==5) escudo2=620; end
		if(p[1].escudo==4) escudo2=650; end
		if(p[1].escudo==3) escudo2=690; end
		if(p[1].escudo==2) escudo2=720; end
		if(p[1].escudo==1) escudo2=750; end
		if(p[1].escudo==0) escudo2=780; end
		define_region(3,escudo2,30,800,60);
		escudo_ant=p[1].escudo;
	end
	frame;
end
end

//-----------------------------------------------------------------------
// Maneja los bonos
//-----------------------------------------------------------------------

process bono(x,y,tipo);

begin
file=fpg_nave;
if(tipo==1) graph=20; end
if(tipo==2) graph=21; end
if(tipo==3) graph=22; end

loop
	if(pausa==0)
		y+=3; angle+=3000;
		if(collision(p[1].id))
			if(tipo==1) p[1].escudo+=1; end
			if(tipo==2) p[1].energia=20; end
			if(tipo==3) p[1].poder+=1; end
			explotalo(x,y,z,alpha,angle,file,graph,20);
			break;
		end
		if(y>650) break; end
	end
	frame;
end
end
