//-----------------------------------------------------------------------
// proceso highscores
//-----------------------------------------------------------------------

process highscores();

private
	a;

begin
	let_me_alone();
	delete_text(all_text);
	
	x=ancho_pantalla/2;
	write(fuente[0],x,150,4,textos[49]);
	
	timer[2]=0;
	while(timer[2]<50)
		scroll.x0+=3;
		frame;
	end
	
	controlador(0);
	
	y=500;
	from a=9 to 0;
		write(fuente[0],x-150,y,3,a+1 + "º " + puntuaciones[a].nombres);
		write(fuente[0],x+150,y,5,puntuaciones[a].puntos);
		y-=30;
		scroll.x0+=3;
		timer[2]=0;
		while(timer[2]<50 and !p[0].botones[b_aceptar] and !p[0].botones[b_cancelar])
			if(!p[0].botones[b_aceptar]) scroll.x0+=3; end
			frame;
		end
		if(p[0].botones[b_cancelar]) menu(0); end
	end
	
	while(p[0].botones[b_aceptar] or p[0].botones[b_cancelar])
		scroll.x0+=3;
		frame;
	end
	while(not p[0].botones[b_aceptar] and not p[0].botones[b_cancelar])
		scroll.x0+=3;
		frame;
	end
	while(p[0].botones[b_aceptar] or p[0].botones[b_cancelar])
		scroll.x0+=3;
		frame;
	end
	menu(0);
end



//-----------------------------------------------------------------------
// proceso nuevo highscore
//-----------------------------------------------------------------------

process nuevo_highscore(nuevo_j1,nuevo_j2,nuevo_j3,nuevo_j4);

private
	a;
	nueva_puntuacion[5];
	nuevo_aspirante[5]=0,-1,-1,-1,-1;

begin
	let_me_alone();
	delete_text(all_text);
	
	fuente_teclado=fuente[0];
	nueva_puntuacion[1]=nuevo_j1;
	nueva_puntuacion[2]=nuevo_j2;
	nueva_puntuacion[3]=nuevo_j3;
	nueva_puntuacion[4]=nuevo_j4;
	
	from jugador=1 to jugadores;
		from a=0 to 9;
			if(nueva_puntuacion[jugador]>=puntuaciones[a].puntos)
				from j=8 to a step -1;
					puntuaciones[j+1].puntos=puntuaciones[j].puntos;
					puntuaciones[j+1].nombres=puntuaciones[j].nombres;
				end
				puntuaciones[a].puntos=nueva_puntuacion[jugador];
				puntuaciones[a].nombres="";
				nuevo_aspirante[jugador]=a;
				break;
			end
		end
		if(jugador>1)
			from a=jugador to 2 step -1;
				if(nuevo_aspirante[a]<=nuevo_aspirante[a-1])
					nuevo_aspirante[a-1]+=1;
				end
			end
		end
	end
	
	from jugador=1 to jugadores;
		if(nuevo_aspirante[jugador]>-1)
			nueva_puntuacion[jugador]=1;
		else	
			nueva_puntuacion[jugador]=0;
		end
	end
	
	if(nuevo_aspirante[1]>-1 or nuevo_aspirante[2]>-1 or nuevo_aspirante[3]>-1 or nuevo_aspirante[4]>-1)
		write(fuente[0],ancho_pantalla/2,70,4,textos[50]);
		text_input(ancho_pantalla/2,400,15,nueva_puntuacion[1],nueva_puntuacion[2],nueva_puntuacion[3],nueva_puntuacion[4]);
		loop
			if(!exists(TYPE text_input)) break; end
			scroll.x0+=3;
			frame;
		end
		from jugador=1 to jugadores;
			if(nuevo_aspirante[jugador]>-1)
				puntuaciones[nuevo_aspirante[jugador]].nombres=texto_introducido[jugador];
			end
		end

		guarda_puntuaciones();
		
		highscores();
	else
		highscores();
	end
end


//-----------------------------------------------------------------------
// proceso escribir archivo
//-----------------------------------------------------------------------

Function guarda_puntuaciones();
Private
	string linea;
Begin
	archivo=fopen(savegamedir+"puntuaciones.csv",O_WRITE);
	from i=1 to 10;
		fputs(archivo,i+"|"+puntuaciones[i-1].nombres+"|"+puntuaciones[i-1].puntos);
	end
	fputs(archivo,"fin");
	fclose(archivo);
end

//-----------------------------------------------------------------------
// funcion hasta la barra
//-----------------------------------------------------------------------

Function string hasta_la_barra(string estring,int num_campo);
private
	string temp;
	w;
	campo_actual;
begin
	from w=0 to len(estring);
		if(estring[w]!="|")
			temp+=""+estring[w];
		else
			if(campo_actual==num_campo)
				break;
			else
				temp="";
				campo_actual++;
			end
		end
	end
	//return atoi(temp);
	return temp;
end

//-----------------------------------------------------------------------
// proceso leer archivo
//-----------------------------------------------------------------------

Function lee_puntuaciones();
Private
	string linea;

Begin
	if(file_exists(savegamedir+"puntuaciones.csv"))
		archivo=fopen(savegamedir+"puntuaciones.csv",O_READ);
	
		while(!feof(archivo))
			linea=fgets(archivo);
			if(find(linea,"fin")>-1) break; end
			
			puntuaciones[i].nombres=hasta_la_barra(linea,1);
			puntuaciones[i].puntos=atoi(hasta_la_barra(linea,2));
						
			i++;
		end
		fclose(archivo);
	else
		puntuaciones[0].nombres="Carles";
		puntuaciones[0].puntos=10000;
		puntuaciones[1].nombres="PiXeL";
		puntuaciones[1].puntos=9000;
		puntuaciones[2].nombres="Nibbler337";
		puntuaciones[2].puntos=8000;
		puntuaciones[3].nombres="Ana";
		puntuaciones[3].puntos=7000;
		puntuaciones[4].nombres="Chewrafa";
		puntuaciones[4].puntos=6000;
		puntuaciones[5].nombres="Nico";
		puntuaciones[5].puntos=5000;
		puntuaciones[6].nombres="PiX";
		puntuaciones[6].puntos=4000;
		puntuaciones[7].nombres="PuX";
		puntuaciones[7].puntos=3000;
		puntuaciones[8].nombres="PaX";
		puntuaciones[8].puntos=2000;
		puntuaciones[9].nombres="PeX";
		puntuaciones[9].puntos=1000;
	end
end