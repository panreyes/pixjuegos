
//-----------------------------------------------------------------------
// Maneja los disparos de la nave protagonista
//-----------------------------------------------------------------------

PROCESS disparo01(x,y,angle)
BEGIN

z-=10;
region=1;
file=fpg_bombas;
if(fuerza==1) graph=5; end
if(fuerza==2) graph=6; end
if(fuerza==3) graph=7; end
if(fuerza==4) graph=8; end

REPEAT
	if(pausa==0)
		advance(30);
	end
	FRAME;
UNTIL (out_region(id,1))
END

//-----------------------------------------------------------------------
// Maneja las bombas
//-----------------------------------------------------------------------

process bomba(tipo);

private time=0; id2;
begin
z+=15;
region=1;
file=fpg_bombas;
if(tipo==1)
	graph=10;
	size=0;
	loop
	if(pausa==0)
		x=father.x;
		y=father.y-30;
		time++;
		size+=10;
		if(time==10) laser(x,y,10,id); suena(s_laser1); break; end

	end
		frame;
	end
end
if(tipo==2)
	graph=20;
	size=0;
	loop
	if(pausa==0)
		x=father.x;
		y=father.y-30;
		time++;
		if(time<20) size+=5; end
		if(time==20) laser(x,y,20,id); end
		if(time>20) laser(x,y,21,id); end
		if(time==25) suena(s_laser2); end
		if(time>125) break; end
	end
	frame;
	end
end

if(tipo==3)
	graph=30;
	loop
	if(pausa==0)
		x=father.x;
		y=father.y-30;
		time++;
		if(time==15) laser(x,y,30,id); suena(s_laser2); end
		if(time>45) break; end

	end
		frame;
	end
end

if(tipo==4)
	x=id_nave.x;
	y=id_nave.y;
	if(exists(type enemigo))
		id2=get_id(type enemigo);
	elseif(exists(type mina))
		id2=get_id(type mina);
	elseif(exists(type asteroide))
		id2=get_id(type asteroide);
	elseif(exists(type boss))
		id2=get_id(type boss);
	end
	laser(x-20,y,40,id2);
	laser(x+20,y,40,id2);
	frame(700);
end

if(tipo==5)
	laser(x,y,50,id_nave);
	while(time<140)
		time++;
		frame;
	end
end

end

//-----------------------------------------------------------------------
// Maneja el laser de las bombas
//-----------------------------------------------------------------------

process laser(x,y,tipo,id2);

private angulo; x1; avance;

begin
z-=15;
region=1;
file=fpg_bombas;

if(tipo==10)
	laser(x,y+70,11,id);
	laser(x,y+70,12,id); 
	size=10;
	graph=11;
	angle=pi/2;
	loop
	if(pausa==0)
		y-=30;
		if(size<100) size+=10; end
		if(out_region(id,1)) break; end
	end
	frame;
	end
end
if(tipo==11)
	angulo=-pi/2;
	graph=12;
	angle=pi/2;
	size=10;
	loop
	if(pausa==0)
		y-=30; 
		x=id2.x+get_distx(angulo,100);
		angulo+=10000;
		if(size<100) size+=10; end
		if(angulo<pi) z=id2.z+1; else z=id2.z-1; end 
		if(y<-50) break; end
	end
	frame;
	end
end
if(tipo==12)
	angulo=pi/2;
	graph=12;
	angle=pi/2;
	size=10;
	loop
	if(pausa==0)
		y-=30; 
		x=id2.x+get_distx(angulo,100);
		angulo+=10000;
		if(size<100) size+=10; end
		if(angulo<pi) z=id2.z+1; else z=id2.z-1; end 
		if(y<-50) break; end
	end
	frame;
	end
end
if(tipo==20)
	graph=21;
	angle=pi/2;
	z=16;
	avance=30;
	loop
	if(pausa==0)
		if(exists(id2))
			x=id2.x;
			y=id2.y+get_distx(angulo,avance);
		else
			size_y-=5;
			y=-30;
		end
				y=id2.y+get_distx(angulo,avance);
		avance-=30;
		if(out_region(id,1)) break; end
	end
	frame;
	end
end
if(tipo==21)
	graph=22;
	angle=pi/2;
	z=16;
	avance=10;
	loop
	if(pausa==0)
		if(exists(id2))
			x=id2.x;
			y=id2.y+get_distx(angulo,avance);
		else
			size_y-=5;
			y-=50;
		end
		avance-=40;
		if(out_region(id,1)) break; end
	end
	frame;
	end
end
if(tipo==30)
	graph=31;
	loop
	if(pausa==0)
		x=father.x;
		y=father.y;
		if(angle<pi) angle+=6000; end
		if(angle=>pi) break; end
		sombra(graph,x,y,file,2);
	end
	frame;
	end
end

if(tipo==40)
	graph=101;
	angle=90000;
	loop
		if(pausa==0)
			angulo=get_angle(id2);
			if(angulo>270000 and angle==90000)
				angle+=360000;
			end
			if(exists(id2))
				if(angulo>angle)
					angle+=10000;
				elseif(angulo<angle)
					angle-=10000;
				end
			end	
			if(x1<20) x1++; end
			advance(x1);
			if(collision (type enemigo)) break; end
			if(collision (type mina)) break; end
			if(collision (type boss)) break; end
			if(collision (type disparo02)) break; end
			if(collision (type bomba02)) break; end
			if(collision (type laser02)) break; end
			if(out_region(id,0)) signal(id,s_kill); end 
			estela(x,y,angle,fpg_explosiones,42);
		end
		frame;
	end
	file=fpg_explosiones;
	graph=1;
	suena(s_explosion);
	repeat
		if(pausa==0) graph++; end
	frame;
	until(graph==12)
	
end

if(tipo==50)
	graph=60;
	flags=0;
	size=0;
	loop
		if(pausa==0)
			x=id_nave.x;
			y=id_nave.y;
			x1++;
			if(size<150) size+=10; end
			if(x1>120) break; end
			if(id2=collision(type disparo02))
				angulo=get_angle(id2)-id2.angle+270000+(get_angle(id2)-90000);
				disparo01(id2.x,id2.y,angulo);
				signal(id2,s_kill);
			end
		end
		frame;
	end
	flags=0;
	alpha=128;
	while(alpha>0)
		if(pausa==0)
			alpha-=20;
			if(id2=collision(type disparo02))
				angulo=get_angle(id2)-id2.angle+270000+(get_angle(id2)-90000);
				disparo01(id2.x,id2.y,angulo);
				signal(id2,s_kill);
			end
			size+=20;
		end
		frame;
	end
end

end

//-----------------------------------------------------------------------
// Maneja los disparos de las naves enemigas
//-----------------------------------------------------------------------

PROCESS disparo02(x,y,angle,tipo);

private id2;

BEGIN

z-=86;
region=1;
file=fpg_bombas;
if(tipo==1) graph=1; end
if(tipo==2) graph=2; end
if(tipo==3) graph=3; end
if(tipo==4) graph=100; end
if(tipo==5) graph=4; end

advance(15);

REPEAT
	if(estado==-1) break; end
	if(pausa==0)
		advance(20);
		if(id2=collision(type laser))
			if(id2.graph!=60)
				explotalo(x,y,z,alpha,angle,file,graph,10);
				break;
			end
		end
		if(tipo==4)
			estela(x,y,angle,fpg_explosiones,42);
		end

		if(collision(id_nave)) id_nave.estado=-1; explosion(x,y,2,75); end
		if(collision(type disparo01)) explosion(x,y,2,75); break; end
	end
	FRAME;
UNTIL (out_region(id,1) or collision(id_nave))
END

//-----------------------------------------------------------------------
// Maneja las bombas enemigas
//-----------------------------------------------------------------------

process bomba02(x1,y1,angle,size);

private time=0; id2;
begin
z-=15;
file=fpg_bombas;
region=1;
id2=father;
graph=40;
loop
	if(pausa==0)
		if(exists(id2))
			x=id2.x+x1;
			y=id2.y+y1;
			time++;
			if(time>20) laser02(x,y,angle,size); end
			if(time==25) suena(s_laser2); end
			if(time>125) break; end
		else
			break;
		end
	end
	frame;
end
end


//-----------------------------------------------------------------------
// Maneja el laser de las bombas enemigas
//-----------------------------------------------------------------------

process laser02(x1,y1,angle,size_y);

private id2; avance;

begin
z-=14;
file=fpg_bombas;
region=1;
graph=41;
id2=father;
avance=30;
loop
	if(pausa==0)
		if(collision(id_nave)) id_nave.estado=-1; explosion(x,y,2,200); end
		if(exists(id2))
			x1=id2.x;
			y1=id2.y;
			x=x1+get_distx(angle,avance);
			y=y1+get_disty(angle,avance);
		else
			size_y-=10;
			x=x1+get_distx(angle,avance);
			y=y1+get_disty(angle,avance);
		end
		avance+=50;
		if(out_region(id,1)) break; end
	end
	frame;
end
end
