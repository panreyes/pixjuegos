
//-----------------------------------------------------------------------
// Maneja la nave protagonista
//-----------------------------------------------------------------------

PROCESS nave01(x,y, jugador)

private acelx; acely; disparando; inmunidad=75; retardo=5; id_bomba;

BEGIN
	graph=1;
	file=fpg_nave;
	controlador(jugador);
	fuerza[jugador]=jugador;
	loop
		LOOP
			if(pausa==0)
				if(inmunidad<75) inmunidad++; estado=0; flags=4; else flags=0; end
				if(retardo<5) retardo++; end
				if(estado==-1 and inmunidad==75) 
					if(escudo[jugador]>0) 
						escudo[jugador]--; 
						inmunidad=0; 
					else 
						explosion(x,y,3,200);
						explotalo(x,y,z,alpha,angle,file,graph,20);
						break; 
					end 
				end
				if(p[jugador].botones[1] and acelx<10) graph=3+((jugador-1) * 3); acelx+=2; end
				if(p[jugador].botones[0] and acelx>-10) graph=2+((jugador-1) * 3); acelx-=2; end
				if(p[jugador].botones[2] and acely>-10) acely-=2; end
				if(p[jugador].botones[3] and acely<10) acely+=2; end

				if(mouse.left)
					if(mouse.x-x<-12 and acelx>-10) graph=2+((jugador-1) * 3); acelx-=3; end 
					if(mouse.x-x>12 and acelx<10) graph=3+((jugador-1) * 3); acelx+=3; end
					if(mouse.y-y<-12 and acely>-10) acely-=3; end
					if(mouse.y-y>12 and acely<10) acely+=3; end
				end
				
				if(x>874) x=874; end
				if(x<150) x=150; end
				if(y<140) y=140; end
				if(y>700) y=700; end

				if(acelx>0 and not p[jugador].botones[1] and x<942) acelx--; end
				if(acelx<0 and not p[jugador].botones[0] and x>70) acelx++; end
				if(acely<0 and not p[jugador].botones[2] and y>150) acely++; end
				if(acely>0 and not p[jugador].botones[3] and y<690) acely--; end

				x+=acelx; y+=acely;
				if(acelx==0)
					graph=1+((jugador-1) * 3);
				end
				IF (p[jugador].botones[4] and disparando==0 and retardo==5)
					retardo=0;
					if(poder[jugador]==1)
						disparo01(x-15,y-10,angle+pi/2);
						disparo01(x+15,y-10,angle+pi/2);
							suena(s_disparo);
					END
					if(poder[jugador]==2)
						disparo01(x,y-15,angle+pi/2);
						disparo01(x-20,y+10,angle+pi/2);
						disparo01(x+20,y+10,angle+pi/2);
						suena(s_disparo);
					END
					if(poder[jugador]==3)
						disparo01(x-30,y+15,angle+pi/2);
						disparo01(x-10,y-10,angle+pi/2);
						disparo01(x+10,y-10,angle+pi/2);
						disparo01(x+30,y+15,angle+pi/2);
						suena(s_disparo);
					END
					if(poder[jugador]==4)
						disparo01(x-30,y+15,angle+pi*2/3);
						disparo01(x-30,y+15,angle+pi/2);
						disparo01(x-10,y-10,angle+pi/2);
						disparo01(x+10,y-10,angle+pi/2);
						disparo01(x+30,y+15,angle+pi/2);
						disparo01(x+30,y+15,angle+pi*1/3);
						suena(s_disparo);
					END
				end
				if(disparando==0 and p[jugador].botones[5])
					if(habil[jugador]==1)
						if(energia[jugador]>4)
							id_bomba=bomba(habil[jugador]); energia[jugador]-=5; disparando=1;
						end
					end
					if(habil[jugador]==2)
						if(energia[jugador]>9)
							id_bomba=bomba(habil[jugador]); energia[jugador]-=10; disparando=1;
						end
					end
					if(habil[jugador]==3)
						if(energia[jugador]>9)
							id_bomba=bomba(habil[jugador]); energia[jugador]-=10; disparando=1;
						end
					end
					if(habil[jugador]==4)
						if(energia[jugador]>=1)
							id_bomba=bomba(habil[jugador]); energia[jugador]-=1; disparando=1;
						end
					end
					if(habil[jugador]==5)
						if(energia[jugador]>6)
							id_bomba=bomba(habil[jugador]); energia[jugador]-=7; disparando=1;
						end
					end
				end
				if(not exists(id_bomba))
					disparando=0;
				end
			end
			FRAME;
		END
		if(poder[jugador]>1) poder[jugador]--; end
		escudo[jugador]=5;
		inmunidad=0;
		estado=0;
		if(vidas[jugador]>-1) vidas[jugador]--; else break; end
		y=650;
		while(y>550) y-=5; frame; end
		frame;
	end
END

//-----------------------------------------------------------------------
// Maneja marcadores
//-----------------------------------------------------------------------

process marcador(jugador);

private gatillo; direccion=1; time; puntos2;

begin
z=-100; file=fpg_menu;
if(jugador==1)
	x=50; y=50;
	write_var(fuente1,100,50,3,puntos2);
end
if(jugador==2)
	x=974; y=50;
	flags=1;
	write_var(fuente1,924,50,3,puntos2);
end
graph=20;

barra_energia(jugador);
barra_escudo(jugador);
habilidad(jugador);

puntos2=puntos[jugador];
loop
	if(pausa==0)

		time++;
		if(time>125 and energia[jugador]<20) energia[jugador]++; time=0; end

		if(direccion==1) graph++; else graph--; end
		if(graph==20) direccion=1; end
		if(graph==44) direccion=0; end

		if(p[jugador].botones[6] and gatillo==0) habil[jugador]++; gatillo=1; end
		if(not p[jugador].botones[6] and gatillo==1) gatillo=0; end
		if(habil[jugador]>5) habil[jugador]=1; end
		if(habil[jugador]<1) habil[jugador]=5; end
		if(escudo[jugador]>5) escudo[jugador]=5; end
		if(poder[jugador]>4) poder[jugador]=4; end
		if(energia[jugador]>20) energia[jugador]=20; end
		if(puntos[jugador]>puntos2) puntos2+=(puntos[jugador]-puntos2)/10; end
	end
	frame;
end
end

//-----------------------------------------------------------------------
// muestra la habilidad
//-----------------------------------------------------------------------

process habilidad(jugador);

begin
	z=-110; file=fpg_nave;
	if(jugador==1)
		x=50; y=50;
	end
	if(jugador==2)
		x=974; y=50;
	end
	loop
		if(habil[jugador]==1) graph=30; end
		if(habil[jugador]==2) graph=31; end
		if(habil[jugador]==3) graph=32; end
		if(habil[jugador]==4) graph=33; end
		if(habil[jugador]==5) graph=34; end
		if(habil[jugador]==6) graph=0; end
		frame;
	end
end

//-----------------------------------------------------------------------
// Maneja la barra de energia
//-----------------------------------------------------------------------
process barra_energia(jugador);

private
energia2;

begin
	z=-110; graph=40; file=fpg_nave;
	if(jugador==1)
		x=50; y=200; 
	end
	if(jugador==2)
		x=974; y=200; flags=1;
	end
	loop
		if(pausa==0)
			if(energia[jugador]>energia2) energia2++; end
			if(energia[jugador]<energia2) energia2--; end
			graph=energia2+40;
		end
		frame;
	end
end

//-----------------------------------------------------------------------
// Maneja la barra de escudo
//-----------------------------------------------------------------------

process barra_escudo(jugador);

private
escudo2;
escudo_ant;

begin
z=-110; graph=25; file=fpg_nave;
if(jugador==1)
	x=50; y=200; 
end
if(jugador==2)
	x=974; y=200; flags=1;
end
loop
	if(pausa==0)

		if(escudo_ant>escudo[jugador])
			if(escudo[jugador]==4)
				if(opciones.particulas==1)
					explotalo(78,y-13,z-1,alpha,angle,file,15,10);
				else
					sombra(graph,78,y-13,file,1);
				end
			end
			if(escudo[jugador]==3)
				if(opciones.particulas==1)
					explotalo(78,y+13,z-1,alpha,angle,file,15,10);
				else
					sombra(graph,78,y+13,file,1);
				end
			end
			if(escudo[jugador]==2)
				if(opciones.particulas==1)
					explotalo(78,y+39,z-1,alpha,angle,file,15,10);
				else
					sombra(graph,78,y+39,file,1);
				end
			end
			if(escudo[jugador]==1)
				if(opciones.particulas==1)
					explotalo(78,y+64,z-1,alpha,angle,file,15,10);
				else
					sombra(graph,78,y+64,file,1);
				end
			end
			if(escudo[jugador]==0)
				if(opciones.particulas==1)
					explotalo(78,y+90,z-1,alpha,angle,file,15,10);
				else
					sombra(graph,78,y+90,file,1);
				end
			end		
		end

		graph=escudo[jugador]+20;
		escudo_ant=escudo[jugador];
	end
	frame;
end
end

//-----------------------------------------------------------------------
// Maneja los bonos
//-----------------------------------------------------------------------

process bono(x,y,tipo);

private
	nave;

begin
file=fpg_nave;
if(tipo==1) graph=15; end
if(tipo==2) graph=16; end
if(tipo==3) graph=17; end

loop
	if(pausa==0)
		y+=3; angle+=3000;
		
		from nave=0 to 3;
			if(exists(id_nave[nave]))
				if(collision(id_nave[nave]))
					if(tipo==1) escudo[nave]+=1; end
					if(tipo==2) energia[nave]=20; end
					if(tipo==3) poder[nave]+=1; end
					if(opciones.particulas==1)
						explotalo(x,y,z,alpha,angle,file,graph,20);
					else
						sombra(graph,735,y,file,1);
					end
					return;
				end
			end
		end

		if(y>800) break; end
	end
	frame;
end
end
