
//-----------------------------------------------------------------------
// Maneja la nave protagonista
//-----------------------------------------------------------------------

PROCESS nave01(x,y, jugador)

private acelx; acely; disparando; inmunidad=75; retardo=5;

BEGIN
	graph=1;
	file=fpg_nave;
	controlador(0);
	loop
		LOOP
			if(pausa==0)
				if(inmunidad<75) inmunidad++; estado=0; flags=4; else flags=0; end
				if(retardo<5) retardo++; end
				if(estado==-1 and inmunidad==75) 
					if(escudo[jugador]>0) 
						escudo[jugador]--; 
						inmunidad=0; 
					else 
						explosion(x,y,3,200);
						explotalo(x,y,z,alpha,angle,file,graph,20);
						break; 
					end 
				end
				if(p[0].botones[1] and acelx<10) graph=3; acelx+=2; end
				if(p[0].botones[0] and acelx>-10) graph=2; acelx-=2; end
				if(p[0].botones[2] and acely>-10) acely-=2; end
				if(p[0].botones[3] and acely<10) acely+=2; end

				if(mouse.left)
					if(mouse.x-x<-12 and acelx>-10) graph=2; acelx-=3; end 
					if(mouse.x-x>12 and acelx<10) graph=3; acelx+=3; end
					if(mouse.y-y<-12 and acely>-10) acely-=3; end
					if(mouse.y-y>12 and acely<10) acely+=3; end
				end
				
				if(x>952) x=952; end
				if(x<60) x=60; end
				if(y<140) y=140; end
				if(y>700) y=700; end

				if(acelx>0 and not p[0].botones[1] and x<942) acelx--; end
				if(acelx<0 and not p[0].botones[0] and x>70) acelx++; end
				if(acely<0 and not p[0].botones[2] and y>150) acely++; end
				if(acely>0 and not p[0].botones[3] and y<690) acely--; end

				x+=acelx; y+=acely;
				if(acelx==0) graph=1; end
				IF (p[0].botones[4] and disparando==0 and retardo==5)
					retardo=0;
					if(poder[jugador]==1)
						disparo01(x-15,y-10,angle+pi/2);
						disparo01(x+15,y-10,angle+pi/2);
							suena(s_disparo);
					END
					if(poder[jugador]==2)
						disparo01(x,y-15,angle+pi/2);
						disparo01(x-20,y+10,angle+pi/2);
						disparo01(x+20,y+10,angle+pi/2);
						suena(s_disparo);
					END
					if(poder[jugador]==3)
						disparo01(x-30,y+15,angle+pi/2);
						disparo01(x-10,y-10,angle+pi/2);
						disparo01(x+10,y-10,angle+pi/2);
						disparo01(x+30,y+15,angle+pi/2);
						suena(s_disparo);
					END
					if(poder[jugador]==4)
						disparo01(x-30,y+15,angle+pi*2/3);
						disparo01(x-30,y+15,angle+pi/2);
						disparo01(x-10,y-10,angle+pi/2);
						disparo01(x+10,y-10,angle+pi/2);
						disparo01(x+30,y+15,angle+pi/2);
						disparo01(x+30,y+15,angle+pi*1/3);
						suena(s_disparo);
					END
				end
				if(disparando==0 and p[0].botones[5])
					if(habil[jugador]==1)
						if(energia>4)
							bomba(habil[jugador]); energia[jugador]-=5; disparando=1;
						end
					end
					if(habil[jugador]==2)
						if(energia>9)
							bomba(habil[jugador]); energia[jugador]-=10; disparando=1;
						end
					end
					if(habil[jugador]==3)
						if(energia>9)
							bomba(habil[jugador]); energia[jugador]-=10; disparando=1;
						end
					end
					if(habil[jugador]==4)
						if(energia>1)
							bomba(habil[jugador]); energia[jugador]-=1; disparando=1;
						end
					end
					if(habil[jugador]==5)
						if(energia[jugador]>6)
							bomba(habil[jugador]); energia[jugador]-=7; disparando=1;
						end
					end
				end
				if(not exists(type bomba))
					disparando=0;
				end
			end
			FRAME;
		END
		if(poder[jugador]>1) poder[jugador]--; end
		escudo[jugador]=5;
		inmunidad=0;
		estado=0;
		if(vidas[jugador]>-1) vidas[jugador]--; else break; end
		if(jugadores==2)
			if(jugador==1)
				x=350;
			else
				x=700;
			end
		else
			x=512;
		end
		y=650;
		while(y>550) y-=5; frame; end
		frame;
	end
END

//-----------------------------------------------------------------------
// Maneja marcadores
//-----------------------------------------------------------------------

process marcador(jugador);

private gatillo; direccion=1; time; puntos2;

begin
x=400; y=50; z=-100; file=fpg_menu;

graph=20;

barra_energia(jugador);
barra_escudo(jugador);
habilidad(jugador);
objeto(100,50,12,fpg_nave,100,0);
objeto(300,50,1,fpg_nave,20,0);
write_var(fuente1,320,50,4,vidas);
write_var(fuente1,600,50,5,puntos2);
puntos2=puntos[jugador];
loop
	if(pausa==0)

		time++;
		if(time>125 and energia[jugador]<20) energia[jugador]++; time=0; end

		if(direccion==1) graph++; else graph--; end
		if(graph==20) direccion=1; end
		if(graph==44) direccion=0; end

		if(p[0].botones[6] and gatillo==0) habil[jugador]++; gatillo=1; end
		if(not p[0].botones[6] and gatillo==1) gatillo=0; end
		if(habil[jugador]>5) habil[jugador]=1; end
		if(habil[jugador]<1) habil[jugador]=5; end
		if(escudo[jugador]>5) escudo[jugador]=5; end
		if(poder[jugador]>4) poder[jugador]=4; end
		if(energia[jugador]>20) energia[jugador]=20; end
		if(puntos[jugador]>puntos2) puntos2+=(puntos[jugador]-puntos2)/10; end
	end
	frame;
end
end

//-----------------------------------------------------------------------
// muestra la habilidad
//-----------------------------------------------------------------------

process habilidad(jugador);

begin
x=400; y=50; z=-110; file=fpg_nave;
loop
	if(habil[jugador]==1) graph=30; end
	if(habil[jugador]==2) graph=31; end
	if(habil[jugador]==3) graph=32; end
	if(habil[jugador]==4) graph=33; end
	if(habil[jugador]==5) graph=34; end
	if(habil[jugador]==6) graph=0; end
 	frame;
end
end

//-----------------------------------------------------------------------
// Maneja la barra de energia
//-----------------------------------------------------------------------
process barra_energia();

private
energia2;

begin
x=100; y=50; z=-110; graph=11; region=2; file=fpg_nave;
loop
	if(pausa==0)
		if(energia[jugador]>energia2) energia2++; end
		if(energia[jugador]<energia2) energia2--; end
		define_region(2,25,30,energia2*150/20,60);
	end
	frame;
end
end

//-----------------------------------------------------------------------
// Maneja la barra de escudo
//-----------------------------------------------------------------------

process barra_escudo();

private
escudo2;
escudo_ant;

begin
x=700; y=50; z=-110; graph=10; region=3; file=fpg_nave;
loop
	if(pausa==0)

		if(escudo_ant>escudo[jugador])
			if(escudo[jugador]==4)
				if(opciones.particulas==1)
					explotalo(635,y,z,alpha,angle,file,20,10);
				else
					sombra(graph,635,y,file,1);
				end
			end
			if(escudo[jugador]==3)
				if(opciones.particulas==1)
					explotalo(670,y,z,alpha,angle,file,20,10);
				else
					sombra(graph,670,y,file,1);
				end
			end
			if(escudo[jugador]==2)
				if(opciones.particulas==1)
					explotalo(705,y,z,alpha,angle,file,20,10);
				else
					sombra(graph,705,y,file,1);
				end
			end
			if(escudo[jugador]==1)
				if(opciones.particulas==1)
					explotalo(735,y,z,alpha,angle,file,20,10);
				else
					sombra(graph,735,y,file,1);
				end
			end
			if(escudo[jugador]==0)
				if(opciones.particulas==1)
					explotalo(765,y,z,alpha,angle,file,20,10);
				else
					sombra(graph,765,y,file,1);
				end
			end		
		end

		if(escudo[jugador]==5) escudo2=620; end
		if(escudo[jugador]==4) escudo2=650; end
		if(escudo[jugador]==3) escudo2=690; end
		if(escudo[jugador]==2) escudo2=720; end
		if(escudo[jugador]==1) escudo2=750; end
		if(escudo[jugador]==0) escudo2=780; end
		define_region(3,escudo2,30,800,60);
		escudo_ant=escudo[jugador];
	end
	frame;
end
end

//-----------------------------------------------------------------------
// Maneja los bonos
//-----------------------------------------------------------------------

process bono(x,y,tipo);

begin
file=fpg_nave;
if(tipo==1) graph=20; end
if(tipo==2) graph=21; end
if(tipo==3) graph=22; end

loop
	if(pausa==0)
		y+=3; angle+=3000;
		if(collision(id_nave[0]))
			if(tipo==1) escudo[1]+=1; end
			if(tipo==2) energia[1]=20; end
			if(tipo==3) poder[1]+=1; end
				if(opciones.particulas==1)
					explotalo(x,y,z,alpha,angle,file,graph,20);
				else
					sombra(graph,735,y,file,1);
				end
			break;
		end
		if(collision(id_nave[1]))
			if(tipo==1) escudo[2]+=1; end
			if(tipo==2) energia[2]=20; end
			if(tipo==3) poder[2]+=1; end
				if(opciones.particulas==1)
					explotalo(x,y,z,alpha,angle,file,graph,20);
				else
					sombra(graph,735,y,file,1);
				end
			break;
		end
		if(y>800) break; end
	end
	frame;
end
end
