// Un fantasma creado por Carles

Process fantasma();
Private
	lao;
	temporizador=-40;
	bolasmax=7;
	id_sombra;
	id_disp;
	id_fondo;
	id_col;
Begin
	contaor=5;
	vidajefefuera=0;
	ready=0;
	x=400;
	y=20;
	fpg_jefe=load_fpg("fpg/monstruos/fantasma.fpg");
	id_fondo=pon_fondo(load_png("fondos/monstruos/fantasma.png"));
	file=fpg_jefe;
	vidajefe();
	graph=1;
	flags=1;
	size=0;
	from alpha=0 to 100 step 1; y++; size++; frame; end
	id_sombra=fantasma_opaco();
	readyando();
	while(ready==0) frame; end
	while(contaor>0)
		velocidad=400;
		while(ready==0 or reloj==1) frame; end
		if(lao==0) x+=2; flags=1; else x-=2; flags=0; end
		if(x>700) lao=1; end
		if(x<100) lao=0; end
		if(temporizador<41) temporizador++; end
		if(bolasmax>0 and temporizador>30 and bolas<12) bolasmax--; temporizador=0; fantasma_bola(rand(1,16)); end
		if(bolasmax==0)
			if(id_col=collision(type disparos))
				id_col.accion=-1;
				contaor--;
				bolasmax=7;
				suena(16);
				frame(400);
			end
		end
		frame;
	end
	suena(17);
	matabolas=1;
	signal(id_sombra,s_kill);
	desvanecerse(x,y,z,1,file,10000,10000);
	from size=100 to 0 step -5; angle+=5000; frame; end
	matabolas=0;
	jefe=0;
	vidajefefuera=1;
	//signal(id_fondo,s_kill);
	if(modo_juego==1) fondos_panic(); end
	if(modo_juego==2) fondos_tour(); end
End

Process fantasma_bola(tamano);
Private
	id_clipbola;
	radio;
	temporizador;
	id_disp;
	rompida;
	lao;
	id_sombra;
	id_col;
Begin
	z=-1;
	lao=rand(0,1);
	angle=rand(0,360)*1000;
	if(tamano<6) tamano=1; end
	if(tamano>6 and tamano<10) tamano=6; end
	if(tamano>10 and tamano<13) tamano=10; end
	if(tamano>13 and tamano<17) tamano=13; end
	bolas++;
	If(tamano==1) graph=701; End
	If(tamano==6) graph=711; End
	If(tamano==10) graph=801; size=33; End
	If(tamano==13) graph=717; End
	alpha=130;
	id_sombra=fantasma_bola_sombra();
	while(temporizador<400)
		while(ready==0) frame; end
		if(id_col=collision(type disparos))
			id_col.accion=-1;
			rompida=1;
		end
		if(matabolas or rompida) break; end
		if(reloj==0) 
			if(radio<240) radio++; end
			if(lao==0) angle+=1000; else angle-=1000; end
			temporizador++; 
		end
		if(!exists(father)) bolas--; return; end
		x=father.x+get_distx(angle,radio/2);
		y=father.y+get_disty(angle,radio/2);
		frame;
	end
	
	bolas--;
	if(rompida==0) bola(x,y,tamano,lao); else suena(2); end
	signal(id_sombra,s_kill);
End

Process fantasma_bola_sombra();
Private
	fuego_anim;
Begin
	z=father.z+1;
	file=fpg_jefe;
	graph=2;
	alpha=128;
	size=130;
	loop
		if(fuego_anim<40) fuego_anim++; else fuego_anim=0; end
		if(fuego_anim<10) graph=2; end
		if(fuego_anim>10 and fuego_anim=<20) graph=3; end
		if(fuego_anim>20 and fuego_anim=<30) graph=4; end
		if(fuego_anim>30 and fuego_anim=<40) graph=3; end
		if(exists(father))
			x=father.x;
			y=father.y;
		else
			return;
		end
		frame;
	end
End

Process fantasma_opaco();
Begin
	z=father.z+1;
	file=fpg_jefe;
	graph=5;
	loop
		from alpha=0 to 200;
			if(exists(father))
				x=father.x;
				y=father.y;
				flags=father.flags;
			else
				return;
			end
			frame;
		end
		from alpha=200 to 0;
			if(exists(father))
				x=father.x;
				y=father.y;
				flags=father.flags;
			else
				return;
			end
			frame;
		end
		frame(12000);
	end
End

process desvanecerse(x,y,z,graffico,file,omega,delta);
private
	angulo;
	altura;
	desfase;
	radio=1;
begin
	alpha=100;
	flags=father.flags;
	loop
		graph=new_map(radio*2+graphic_info(file,graffico,g_wide),graphic_info(file,graffico,g_height),16);

		angulo=desfase;
		from altura=0 to graphic_info(0,graffico,g_height);
			map_block_copy(file,graph,radio+get_distx(angulo,radio),altura,graffico,0,altura,graphic_info(0,graffico,g_wide),1,128);
			angulo+=omega;
		end
		desfase+=delta;
		if(radio<256) radio++; end
		alpha--;
		if(angulo>360000) angulo=0; end
		if(desfase>360000) desfase=0; end
		frame;
		unload_map(0,graph);
		if(alpha<0) return; end
	end
end
