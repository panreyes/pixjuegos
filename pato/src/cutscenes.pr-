Global
	id_por_detras;
End

Process cutscene_principio();
Private
	id_por_delante;
	id_mano;
	id_taza;
	id_sonido;
Begin
	if(ops.truco_ruzafa==1 or pocos_recursos)
		jugar();
		return;
	end

	include "cutscene_compartido.pr-";

	id_mano.graph=0;
	id_taza.graph=0;
	id_por_delante.graph=6; //ve un nuevo detalle, su estatua la está destruyendo
	frame(10000);
	sonido(24,0);
	id_mano.graph=23; id_taza.graph=25;
	id_taza.x=350;
	id_por_delante.graph=26; //se acojona y se le cae el café
	from i=id_taza.y to 500 step 4;
		id_taza.y=i;
		frame;
	end
	//frame(10000);
	fade_off(); while(fading) frame; end
	id_taza.accion=-1;
	id_mano.accion=-1;	
	//en la habitación del armario, va hacia el armario
	fade_on();
	por_detras(7);
	flags=1;
	id_por_delante.graph=0;
	y=150;
	graph=11;
	from x=448 to 260 step -8;
		if(anim<2)
			anim++;
		else
			anim=0;
			graph++;
			if(graph==16) graph=11; end
		end
		y++;
		frame;
	end
	
	//mira el armario
	graph=0;
	por_detras(8);
	id_por_delante.graph=0;
	frame(10000);
	
	//en la habitación del armario, sale hacia la puerta
	por_detras(7);
	flags=0;
	graph=81;

	from x=260 to 448 step 8;
		if(anim<2)
			anim++;
		else
			anim=0;
			graph++;
			if(graph==86) graph=81; end
		end
		y--;
		frame;
	end

	//de la puerta de la calle hacia fuera
	por_detras(9);
	flags=0;
	id_por_delante.graph=109;
	y=180;
	from x=120 to 800 step 8;
		if(anim<2)
			anim++;
		else
			anim=0;
			graph++;
			if(graph==86) graph=81; end
		end
		if(x>450 and !fading) fade_off(); end
		frame;
	end
	while(fading) frame; end

	jugar();
End

Process cutscene_final();
Private
	id_por_delante;
	id_sonido;
	id_mano;
	id_taza;
Begin
	resolution=global_resolution;
	delete_text(all_text);

	include "cutscene_compartido.pr-";
	
	id_mano.graph=0;
	id_taza.graph=0;
	id_por_delante.graph=10; //ve un nuevo detalle, la estatua de tombatossals está destruyendo la ciudad
	frame(10000);
	sonido(25,0);
	id_mano.graph=23; 
	id_taza.graph=24;
	id_por_delante.graph=27; //cara de DA FUQ?
	from i=320 to 500 step 4;
		id_mano.z=-105;
		id_mano.x=240;
		id_mano.y=i;
		id_taza.z=id_mano.z+1;
		id_taza.x=id_mano.x+100;
		id_taza.y=id_mano.y-40;
		frame;
	end
	frame(3000);
	//frame(20000);
	fade_off(); while(fading) frame; end
	id_taza.accion=-1;
	id_mano.accion=-1;
	//está en la ventana y se va hasta el sofá
	por_detras(4);
	flags=0;
	id_por_delante.graph=0;
	fade_on();
	from x=80 to 437 step 3;
		if(anim<4)
			anim++;
		else
			anim=0;
			graph++;
			if(graph==46) graph=41; end
		end
		if(x<200) y+=3; end
		frame;
	end
	while(y>200)
		if(anim<4)
			anim++;
		else
			anim=0;
			graph++;
			if(graph==46) graph=41; end
		end
		y-=4; 
		frame; 
	end
	id_por_delante.graph=104; //mira por la ventana
	from graph=71 to 73;
		x-=50;
		frame(2000);
	end
	frame(4000);
	graph=74;
	frame(2000);
	graph=73;
	fade_off();
	while(fading) frame; end
		
	//y créditos!
	creditos();
End

Process por_detras(graph);
Begin
	if(exists(id_por_detras)) signal(id_por_detras,s_kill); end
	id_por_detras=id;
	resolution=global_resolution;
	x=ancho_pantalla/2;
	y=alto_pantalla/2;
	z=100;
	file=fpg_cutscenes;
	while(accion==0)
		frame;
	end
End

Process por_delante(graph);
Begin
	resolution=global_resolution;
	x=ancho_pantalla/2;
	y=alto_pantalla/2;
	z=-100;
	file=fpg_cutscenes;
	while(accion==0)
		frame;
	end
End

Process cambia_fps();
Begin
	resolution=global_resolution;
	loop
		if(mouse.left or p[0].botones[b_1] or p[0].botones[b_2] or p[0].botones[b_3])
			i++;
		else
			i=0;
		end
		if(p[0].botones[b_salir] or i>15)
			fade_off();
			while(fading) frame; end
			let_me_alone();
			if(nivel>1)
				creditos();
			else
				jugar();
			end
		end
		if(key(_space))
			set_fps(120,9);
		else
			set_fps(30,0);
		end
		frame;
	end
End

Process creditos();
Private
	id_siguiente_imagen;
	salir_creditos;
Begin
	resolution=global_resolution;
	fade_off();
	while(fading) frame; end
	let_me_alone();
	delete_text(all_text);
	stop_scroll(0);
	fpg_nivel=fpg_cutscenes=load_fpg("fpg/cutscenes.fpg");
	pon_musica(15);
	tv();
	fade_on();
	while(fading) frame; end
	x=ancho_pantalla/2;
	y=alto_pantalla/2;
	z=1;
	file=fpg_cutscenes;
	controlador(0);
	from i=34 to 40;
		id_siguiente_imagen=siguiente_imagen(i);
		while(id_siguiente_imagen.alpha<255 and salir_creditos==0)
			frame; 
		if(p[0].botones[b_salir]) salir_creditos=1; end end
		graph=i;
		id_siguiente_imagen.accion=-1;
		timer[0]=0;
		while(timer[0]<500 and salir_creditos==0)
			if(p[0].botones[b_salir]) salir_creditos=1; end
			frame; 
		end
		if(salir_creditos) break; end
		frame;
	end
	//desbloqueamos el modo matajefes si no está desbloqueado
	if(ops.matajefes==0)
		ops.matajefes=1; //modo matajefes disponible
		guarda_opciones();
		truco_descubierto(textos[4]);
		while(exists(type truco_descubierto)) frame; end
 	end

	fade_off();
	while(fading) frame; end
	menu(-1);
	return;
End

Process siguiente_imagen(graph);
Begin
	resolution=global_resolution;
	file=fpg_cutscenes;
	x=ancho_pantalla/2;
	y=alto_pantalla/2;
	z=-1;
	from alpha=0 to 255 step 5; frame; end
	while(accion==0) frame; end
End

Process tv();
Begin
	resolution=global_resolution;
	graph=33;
	file=fpg_cutscenes;
	x=ancho_pantalla/2;
	y=alto_pantalla/2;
	z=-2;
	while(exists(father)) frame; end
End