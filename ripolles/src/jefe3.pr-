Process jefe3(x);
Private
	objetivo;
	retraso;
	ataque_anterior;
Begin
	resolution=global_resolution;
	while(!exists(id_camara)) frame; end
	priority=1;
	ctype=coordenadas;
	y_base=240;
	y=-300;
	z=-240;
	x=530; //x fija para este 
	fpg_jefe=load_fpg("jefe3.fpg");
	file=fpg_jefe;
	graph=1;
	enemigos++;
	jugador=100;
	p[jugador].vida=1200+(300*(jugadores));
	marcador_jefe();
	accion=quieto;
	rango=60;

	if(global_resolution!=0) j=114; else j=57; end
	from i=61 to 64;
		set_center(file,i,graphic_info(file,i,G_WIDTH)/2,graphic_info(file,i,G_HEIGHT)-j);
	end
	
	i=0; j=0;
	jefe3_sombra();
	while(p[jugador].vida>0)
		while(!ready) frame; end
		anim++;
		altura=y-y_base;
		if(accion!=quieto)
			herida=0;
		end
		retraso++;
		if(accion!=ataca_aire)
			if(id_col=collision(type ataque))
				if(id_col.jugador!=jugador)
					if(en_rango(id_col.z,z,30))
						efecto_golpe(id_col);
						herida=id_col.herida;
						sonido(5,0);
					end
				end
			end

			if(herida>0)
				p[jugador].vida-=herida;
				graph=11;
				while(herida>0)
					while(!ready) frame; end
					herida--;
					anim++;
					if(anim<3)
						x++;
						y--;
					elseif(anim<6) 
						x+=2;
						y-=2;
					elseif(anim<9)
						x--;
						y++;
					else
						anim=0;
					end
					frame;
				end
				i=600;
			end
		end
		switch(accion)
			case quieto:
				if(anim<12) 
					graph=1; 
				elseif(anim<24) 
					graph=2; 
				else
					anim=1; graph=1; 
				end
				
				if(y<130)
					x=530;
					if(gravedad<0) gravedad=0; end
					gravedad++;
					y+=gravedad;
					if(y>130) y=130; end
				else
					gravedad=0;
				end		
			
				if(i<300)
					i++;
				else
					i=0;
					if(ataque_anterior!=ataca_suelo)
						ataque_anterior=ataca_suelo;
						accion=ataca_suelo;
					else
						ataque_anterior=ataca_aire;
						accion=ataca_aire;
					end
				end
				
				if(enemigos==1)
					enemigo(11,1,x-ancho_pantalla,y,0,0);
					enemigo(12,1,x+ancho_pantalla,y,0,1);
					enemigo(13,1,x-ancho_pantalla,y,0,0);
					enemigo(14,1,x+ancho_pantalla,y,0,1);
				end
			end
			case ataca_aire:
				if(y>-500)
					if(gravedad>0) gravedad=0; end
					gravedad-=3;
					y+=gravedad;
				else
					gravedad=0;
				end

				if(j-2<jugadores)
					if(retraso>120)
						retraso=0;
						j++;
						mano_vertical();
					end
				end

				if(i<500)
					i++;
				else
					i=0;
					accion=quieto;
				end
			end
			case ataca_suelo:
				if(i<500)
					i++;
				else
					i=0;
					accion=quieto;
				end

				
				if(y>-500)
					if(gravedad>0) gravedad=0; end
					gravedad-=3;
					y+=gravedad;
				else
					gravedad=0;
				end


				if(j<jugadores)
					if(retraso>70)
						retraso=0;
						j++;
						switch(rand(0,2))
							case 0: mano_lateral(187); end
							case 1: mano_lateral(257); end
							case 2: mano_lateral(310); end
						end			
					end
				end
			end
		end
		frame;
	end
	sonido(6,0);
	ganando=1;
	graph=11;

	suma_puntos(0,20000);

	while(y<130)
		while(!ready) frame; end
		if(gravedad<0) gravedad=0; end
		gravedad++;
		y+=gravedad;
		if(y>130) y=130; end
		frame;
	end	
	jefe3_bien();
	while(graph!=21)
		frame;
	end
	mano_lateral_win(187);
	mano_lateral_win(257);
	mano_lateral_win(310);
	loop
		frame;
	end
End

Process jefe3_bien();
Begin
	resolution=global_resolution;
	ctype=coordenadas;
	file=fpg_jefe;
	graph=21;
	x=father.x;
	y=father.y;
	z=father.z-1;
	from alpha=0 to 255 step 2; frame; end
	father.graph=21;
End

Process mano_lateral(y_base);
Private
	ha_atacado;
Begin
	resolution=global_resolution;
	ctype=coordenadas;
	file=father.file;
	graph=31;
	z=-y_base;
	y=y_base;
	x=-300;
	size_y=50;
	x_inc=30;
	sombra_mano_lateral();
	loop
		while(!ready) frame; end
		if(exists(father))
			if(father.accion!=ataca_suelo) accion=muere; end
		else
			accion=muere;
		end
		if(p[100].vida=<0) accion=muere; end
		if(anim>4)
			anim=0;
			graph++;
			if(accion==ataca_suelo and graph==44) accion=quieto; end
			if(accion==ataca_aire and graph==55) accion=quieto; end
			
			switch(accion)
				case QUIETO:
					if(graph<31 or graph>34) graph=31; end
				end
				case ATACA_SUELO:
					if(graph<41 or graph>43) graph=41; end
				end
				case ATACA_AIRE:
					if(graph<51 or graph>54) graph=51; end
				end
			end
		else
			anim++;
		end
		i++;

		if(id_col=collision_box(type ataque))
			if(id_col.jugador<10 and id_col.jugador>=0)
				if(en_rango(z,id_col.z,id_col.rango))
					p[100].vida-=id_col.herida/2;
					sonido(5,0);
					accion=muere;
				end
			end
		end

		
		if(accion==quieto)
			if(ha_atacado)
				if(x>-300)
					x-=10;
				else 
					break; 
				end
			else
				if(x<-120)
					x+=x_inc;
					if(x_inc>0) x_inc-=4; end
					if(x_inc<0) x_inc=0; end
				end
				if(i>40)
					if(rand(0,1))
						accion=ataca_suelo;
					else
						accion=ataca_aire;
					end			
				end
			end
		else //atacando
			ha_atacado=1;
			if(x<240)
				x_inc+=2;
				x+=x_inc;
			end
			ataque(x+310,y,fpg_general,1,30,20,-1,1);
		end
		if(accion==muere)
			break;
		end
		frame;	
	end
	while(x>-300)
		while(!ready) frame; end
		if(accion!=muere)
			x-=10;
			if(id_col=collision_box(type ataque))
				if(id_col.jugador<10 and id_col.jugador>=0)
					if(en_rango(z,id_col.z,id_col.rango))
						p[100].vida-=id_col.herida/2;
						sonido(5,0);
						accion=muere;
					end
				end
			end
		else
			x-=20;
		end
		frame;
	end
	if(exists(father)) father.j--; end
End

Process sombra_mano_lateral();
Begin
	resolution=global_resolution;
	file=father.file;
	ctype=coordenadas;
	graph=91;
	size_y=50;
	y=father.y+30;
	while(exists(father))
		while(!ready) frame; end
		x=father.x;
		if(anim==4)
			j++;
			if(j==4) j=0; end
			anim=0;
			switch(j)
				case 0: y++; end
				case 1: y--; end
				case 2: y--; end
				case 3: y++; end
			end
		else
			anim++;
		end
		z=200;
		frame;
	end
End

Process mano_lateral_win(y_base);
Private
	ha_atacado;
Begin
	resolution=global_resolution;
	ctype=coordenadas;
	file=father.file;
	graph=31;
	z=-y_base;
	y=y_base;
	x=-300;
	size_y=50;
	x_inc=30;
	sombra_mano_lateral();
	graph=71;
	while(x<120)
		while(!ready) frame; end
		x+=x_inc;
		if(x_inc>0) x_inc++; end
		if(x<-120) 
			graph=81;
		elseif(x<60) 
			graph=82;
		elseif(x<140) 
			graph=83;
		end
		if(x>120) x=120; end
		frame;
	end
	graph=83;
	frame(2000);
	loop
		frame;
	end
End

Process mano_vertical();
Begin
	resolution=global_resolution;
	ctype=coordenadas;
	i=jugador_aleatorio();
	x=p[i].identificador.x;
	y_base=p[i].identificador.y_base;
	z=-y_base;
	y=-500;
	altura=300;
	jugador=100;
	file=fpg_jefe;
	size_x=50;
	sombra_mano_vertical();
	while(son.alpha<255) 
		while(!ready) frame; end
		frame; 
	end
	while(altura>0)
		while(!ready) frame; end
		if(exists(father))
			if(father.accion!=ataca_aire) break; end
		else
			break;
		end

		if(altura<200)
			graph=61;
		elseif(altura<100)
			graph=62;
		else
			graph=63;
		end
		gravedad++;
		altura-=gravedad;
		if(altura<0) altura=0; end
		y=y_base-altura;
		ataque(x,y,fpg_general,1,30,20,-1,1);
		frame;
	end
	graph=64;
	sonido(4,0);
	nube_humo(x,y+35,z,3,-2,rand(70,100));
	nube_humo(x,y+35,z,1,-1,rand(70,100));
	nube_humo(x,y+35,z,-1,-1,rand(70,100));
	nube_humo(x,y+35,z,-3,-2,rand(70,100));
	frame;
	i=0;
	while(i<50)
		while(!ready) frame; end
		if(exists(father))
			if(father.accion!=ataca_aire) break; end
		else
			break;
		end

		i++;
		if(id_col=collision_box(type ataque))
			if(id_col.jugador!=jugador)
				if(en_rango(id_col.z,z,30))
					efecto_golpe(id_col);
					p[jugador].vida-=id_col.herida/2;
					sonido(5,0);
					accion=muere;
					break;
				end
			end
		end
		frame;
	end
	while(altura<300)
		while(!ready) frame; end
		if(accion==muere)
			altura+=15;
		else
			if(id_col=collision_box(type ataque))
				if(id_col.jugador!=jugador)
					if(en_rango(id_col.z,z,30))
						efecto_golpe(id_col);
						herida=id_col.herida/2;
						sonido(5,0);
						accion=muere;
					end
				end
			end
			altura+=5;
		end
		y=y_base-altura;
		frame;
	end
	if(exists(father)) father.j--; end
End

Process sombra_mano_vertical();
Begin
	resolution=global_resolution;
	file=father.file;
	ctype=coordenadas;
	y=father.y_base+53;
	j=0;
	z=-y;
	graph=101;
	alpha=0;
	z=200;
	i=father.i;
	while(exists(father))
		while(!ready) frame; end
		x=father.x;
		if(alpha<255)
			if(exists(p[i].identificador))
				x=p[i].identificador.x;
				y_base=p[i].identificador.y_base;
				y=y_base+53;
				father.y_base=y_base;
				father.x=x;
				father.z=-y_base;
			end
			alpha+=5; 
		end
		//alpha=350-father.altura;
		size=30+father.altura/5;
		frame;
	end
	from alpha=255 to 0 step -20; frame; end
End

Process jefe3_sombra();
Begin
	resolution=global_resolution;
	i=53;
	ctype=coordenadas;
	file=fpg_general;
	graph=3;
	while(exists(father))
		while(!ready) frame; end
		y=father.y_base+i;
		z=200;
		x=father.x;
		altura=father.altura;
		alpha=father.alpha+altura-50;
		size=100+(-altura/3);
		frame;
	end
End
