#IFDEF TACTIL
import "mod_multi";
#ENDIF

Const
	b_izquierda=0;
	b_derecha=1;
	b_arriba=2;
	b_abajo=3;
	b_1=4;
	b_2=5;
	b_3=6;
	b_salir=7;
End

Global
	graph_dedo;
	graph_boton_1;
	graph_boton_2;
	graph_boton_3;
	
	graph_boton_izquierda;
	graph_boton_derecha;
	graph_boton_arriba;
	graph_boton_abajo;
	
	
	id_gamepad;
	gamepad_boton_separacion=40;
	gamepad_boton_size=40;
	gamepad_botones=3;
	
	key_b_izquierda=_LEFT;
	key_b_derecha=_RIGHT;
	key_b_arriba=_UP;
	key_b_abajo=_DOWN;
	key_b_1=_A;
	key_b_2=_S;
	key_b_3=_D;
	key_b_salir=_ESC;
	
	cadena="abcdefghijklmnñopqrstuvwxyzçABCDEFGHIJKLMNÑOPQRSTUVWXYZÇ1234567890?!& áéíóúÁÉÍÓÚ.,";
	
//			a b c d e f g h i j k l m n
//			ñ o p q r s t u v w x y z ç
//			A B C D E F G H I J K L M N
//			Ñ O P Q R S T U V W X Y Z Ç
//			1 2 3 4 5 6 7 8 9 0 ? ! &  
//			á é í ó ú Á É Í Ó Ú . ,BorFin
	
	size_fuente=50; //tamaño de las casillas del teclado
	filas_cadena=6;	//filas en las que se dividira la cadena(la última debe estar completa)
	string texto_introducido[5]; //aqui esta el texto que se introduce
	fuente_teclado;	//Fuente del text_input
	
End

Process controlador(jugador);
Private
	pov;
	povy;
	povx;
	axis_x;
	axis_y;
Begin
	priority=10;
	if(os_id==1003 and jugador==1 and p[1].control==0 and posibles_jugadores==1)
		if(ancho_pantalla==0 or alto_pantalla==0)
			ancho_pantalla=graphic_info(0,0,g_width);
			alto_pantalla=graphic_info(0,0,g_height);
		end
		if(graph_dedo<1)
			pinta_gamepad();			
		end
		if(!exists(id_gamepad))
			id_gamepad=gamepad_virtual();
		end
	End
	
	Loop
		if(os_id==1003 and !focus_status) //en android, si estamos en 2º plano, nos vamos al aire
			let_me_alone();
			fade_music_off(500);
			while(is_playing_song()) frame; end
			exit();
		end

		if(!exists(father)) return; end
		from i=0 to 7; p[jugador].botones[i]=0;	end		
		if(p[jugador].control==-2) //GLOBAL: Para menús y tal
			priority=1;
			from i=1 to posibles_jugadores+1;
				controlador(i);
			end
			loop
				from j=0 to 7;
					p[0].botones[j]=0;
				end
				from i=1 to posibles_jugadores;
					from j=0 to 7;
						if(p[i].botones[j]) p[0].botones[j]=1; end
					end
				end
				frame;
			end
		end		
		if(p[jugador].control==-1) return; end
		If(p[jugador].control==0)  // teclado
			If(key(key_b_izquierda)) p[jugador].botones[0]=1; End
			If(key(key_b_derecha)) p[jugador].botones[1]=1; End
			If(key(key_b_arriba)) p[jugador].botones[2]=1; End
			If(key(key_b_abajo)) p[jugador].botones[3]=1; End
			If(key(key_b_1)) p[jugador].botones[4]=1; End
			If(key(key_b_2)) p[jugador].botones[5]=1; End
			If(key(key_b_3)) p[jugador].botones[6]=1; End
			if(key(key_b_salir)) p[jugador].botones[7]=1; end
		End
		If(p[jugador].control>0)  // joysticks
			POV=joy_gethat(joysticks[p[jugador].control],0); POVx=0; POVy=0;
			
			if(os_id==os_caanoo)
				axis_x = joy_getaxis(2,0);
				axis_y = joy_getaxis(2,1);
	
				// EJES (en teoría debería ser lo mismo que analógico :P)
				if(axis_x<-16384) POVx=1; end
				if(axis_x>16384) POVx=2; end
				if(axis_y<-16384) POVy=1; end
				if(axis_y>16384) POVy=2; end
			end

			// ANALOGICO
			IF (get_joy_position(joysticks[p[jugador].control],0)<-10000) POVx=1; END // izquierda
			IF (get_joy_position(joysticks[p[jugador].control],0)>10000) POVx=2; END // derecha
			IF (get_joy_position(joysticks[p[jugador].control],1)<-7500) POVy=1; END // arriba
			IF (get_joy_position(joysticks[p[jugador].control],1)>7500) POVy=2; END // abajo

			// DIGITAL (LINUX?)
			IF (get_joy_position(joysticks[p[jugador].control],3)<-10000) POVx=1; END // izquierda
			IF (get_joy_position(joysticks[p[jugador].control],3)>10000) POVx=2; END // derecha
			IF (get_joy_position(joysticks[p[jugador].control],4)<-7500) POVy=1; END // arriba
			IF (get_joy_position(joysticks[p[jugador].control],4)>7500) POVy=2; END // abajo
 
			// DIGITAL
			IF (POV==1) POVy=1; END // arriba
			IF (POV==3) POVy=1; POVx=2; END // der
			IF (POV==9) POVy=1; POVx=1; END // izq
			IF (POV==4) POVy=2; END // abajo
			IF (POV==6) POVy=2; POVx=2; END // der
			IF (POV==12) POVy=2; POVx=1; END // izq
			IF (POV==2) POVx=2; END // der
			IF (POV==8) POVx=1; END // izq
		
			If(povx==1) p[jugador].botones[0]=1; End
			If(povx==2) p[jugador].botones[1]=1; End
			If(povy==1) p[jugador].botones[2]=1; End
			If(povy==2) p[jugador].botones[3]=1; End
		
			if(os_id==os_wii)
				If(get_joy_button(joysticks[p[jugador].control],2)) p[jugador].botones[4]=1; End
				If(get_joy_button(joysticks[p[jugador].control],3)) p[jugador].botones[5]=1; End
				If(get_joy_button(joysticks[p[jugador].control],1)) p[jugador].botones[6]=1; End
				If(get_joy_button(joysticks[p[jugador].control],6)) p[jugador].botones[7]=1; End
			elseif(os_id==os_caanoo)
			//elseif(os_id==os_caanoo or os_id==os_dingux_a320 or os_id==os_gp2x or os_id==os_gp2x_wiz or os_id==os_gp32 or os_id==os_dc)
				If(joy_getbutton(0,12)) p[jugador].botones[4]=1; End
				If(joy_getbutton(0,14)) p[jugador].botones[5]=1; End
				If(joy_getbutton(0,13)) p[jugador].botones[6]=1; End
				If(joy_getbutton(0,8)) p[jugador].botones[7]=1; End
			elseif(os_id==1003)
				If(joy_getbutton(joysticks[p[jugador].control],0)) p[jugador].botones[b_arriba]=1; End
				If(joy_getbutton(joysticks[p[jugador].control],1)) p[jugador].botones[b_abajo]=1; End
				If(joy_getbutton(joysticks[p[jugador].control],2)) p[jugador].botones[b_izquierda]=1; End
				If(joy_getbutton(joysticks[p[jugador].control],3)) p[jugador].botones[b_derecha]=1; End
				If(joy_getbutton(joysticks[p[jugador].control],8) or joy_getbutton(joysticks[p[jugador].control],20)) p[jugador].botones[b_1]=1; End
				If(joy_getbutton(joysticks[p[jugador].control],5) or joy_getbutton(joysticks[p[jugador].control],21)) p[jugador].botones[b_2]=1; End
				If(joy_getbutton(joysticks[p[jugador].control],6) or joy_getbutton(joysticks[p[jugador].control],22)) p[jugador].botones[b_3]=1; End
				If(joy_getbutton(joysticks[p[jugador].control],19) or joy_getbutton(joysticks[p[jugador].control],9)) p[jugador].botones[b_salir]=1; End
				if(key(key_b_salir)) p[jugador].botones[b_salir]=1; end
			else
				If(get_joy_button(joysticks[p[jugador].control],0)) p[jugador].botones[4]=1; End
				If(get_joy_button(joysticks[p[jugador].control],1)) p[jugador].botones[5]=1; End
				If(get_joy_button(joysticks[p[jugador].control],2)) p[jugador].botones[6]=1; End
				If(get_joy_button(joysticks[p[jugador].control],8)) p[jugador].botones[7]=1; End
			end
		End
		Frame;
	End
End

Function configurar_controles();
Begin
	//1. Averiguamos cuantos joysticks tenemos
	njoys=number_joy();
	
	//1.1 En Android, descontamos el último joystick ya que se trata de un sensor
	if(os_id==1003)
		njoys=njoys-1;
	end

	//2. ¿Cuántos de ellos son utilizables/tienen al menos dos ejes y dos botones?
	from i=0 to (njoys-1);
		if(joy_numbuttons(i)=>2)
			posibles_jugadores++;
			joysticks[posibles_jugadores]=i;
		end
	end
	
	posibles_jugadores++; //+1 por el jugador en teclado
	//3. Si tenemos X joysticks, X jugadores jugarán con joystick y el siguiente jugará con teclado
	
	//por defecto no tendrán ningún control asignado
	from i=1 to 5; p[i].control=-1; end
	
	if(posibles_jugadores==1) //no hay joysticks
		p[1].control=0;
	end
	if(posibles_jugadores==2) //hay 1 joysticks
		p[1].control=1;
		p[2].control=0;
	end
	if(posibles_jugadores==3) //hay 2 joysticks
		p[1].control=1;
		p[2].control=2;
		p[3].control=0;
	end
	if(posibles_jugadores==4) //hay 3 joysticks
		p[1].control=1;
		p[2].control=2;
		p[3].control=3;
		p[4].control=0;
	end
	if(posibles_jugadores>4) //hay 4 joysticks
		p[1].control=1;
		p[2].control=2;
		p[3].control=3;
		p[4].control=4;
		p[5].control=0;
	end

	//si estamos con la recreativa, no dejaremos jugar con el teclado
	//if(arcade_mode) posibles_jugadores--; end
	//o si, por si acaso
	
	p[0].control=-2; //Esto son los menus :)
	
	//controladores especiales!
	//if(os_id==os_caanoo) //caanoo
	if(os_id==os_caanoo or os_id==os_dingux_a320 or os_id==os_gp2x or os_id==os_gp2x_wiz or os_id==os_gp32 or os_id==os_dc)
		joysticks[0]=1;
		p[1].control=1;
		posibles_jugadores=1;
	end
	
	if(os_id==os_wii) //wii
		joysticks[0]=0;
//		joysticks[1]=1; //por ahora no funciona más de un jugador :(
//		joysticks[2]=2;
//		joysticks[3]=3;
		p[1].control=1;
		p[2].control=2;
		p[3].control=3;
		p[4].control=4;
//		posibles_jugadores=4;
		posibles_jugadores=1;
	end
	
	if(os_id==1003) //android!
		key_b_salir=102;
		if(posibles_jugadores>1) //con joysticks, sin gamepadvirtual
			posibles_jugadores--;
		else
			p[1].control=0;
		end
	end
	
	//Y creo que ya estamos :)
End

Process gamepad_virtual();
Begin
	boton_tactil(gamepad_boton_separacion*2,alto_pantalla-(gamepad_boton_separacion*3),graph_boton_arriba,1,b_arriba);
	boton_tactil(gamepad_boton_separacion*1,alto_pantalla-(gamepad_boton_separacion*2),graph_boton_izquierda,1,b_izquierda);
	boton_tactil(gamepad_boton_separacion*3,alto_pantalla-(gamepad_boton_separacion*2),graph_boton_derecha,1,b_derecha);
	boton_tactil(gamepad_boton_separacion*2,alto_pantalla-gamepad_boton_separacion,graph_boton_abajo,1,b_abajo);
	
	boton_tactil(ancho_pantalla-(gamepad_boton_separacion*2)+get_distx(7*pi/6,gamepad_boton_separacion),alto_pantalla-(gamepad_boton_separacion*2)+get_disty(7*pi/6,gamepad_boton_separacion*2/3),graph_boton_1,1,b_1);
	boton_tactil(ancho_pantalla-(gamepad_boton_separacion*2)+get_distx(11*pi/6,gamepad_boton_separacion),(alto_pantalla-gamepad_boton_separacion*2)+get_disty(11*pi/6,gamepad_boton_separacion*2/3),graph_boton_2,1,b_2);
	if(gamepad_botones==3)
		boton_tactil(ancho_pantalla-(gamepad_boton_separacion*2)+get_distx(pi/2,gamepad_boton_separacion/1),alto_pantalla-(gamepad_boton_separacion*2)+get_disty(pi/2,gamepad_boton_separacion/1),graph_boton_3,1,b_3);
	end
	
	priority=8;
	loop
		if(mouse.left) dedo(mouse.x,mouse.y); end
#IFDEF TACTIL
		for(i=0; i<10; i++)
			if(multi_info(i, "ACTIVE") > 0)
				dedo(multi_info(i, "X"),multi_info(i, "Y"));
			end
		end
#ENDIF
		frame;
	end
End

Process dedo(x,y);
Begin
	priority=6;
	graph=graph_dedo;
	//alpha=0;
	frame;
End

Process boton_tactil(x,y,graph,jugador,boton);
Begin
	priority=4;
	z=-512;
	alpha=128;
	while(exists(father))
		if(collision_box(type dedo))
			p[jugador].botones[boton]=1;
		end
		frame; 
	end
End

Function pinta_gamepad();
Begin
	graph_dedo=new_map(gamepad_boton_size*2/3,gamepad_boton_size*2/3,16);
	drawing_color(rgb(128,128,128));
	drawing_map(0,graph_dedo);
//	draw_box(0,0,gamepad_boton_size/2,gamepad_boton_size*2/3);
	draw_fcircle(gamepad_boton_size/3,gamepad_boton_size/3,gamepad_boton_size/3-1);

	if(os_id!=1003) mouse.graph=graph_dedo; end
	
	graph_boton_1=new_map(gamepad_boton_size*1.5,gamepad_boton_size*1.5,16);
	drawing_map(0,graph_boton_1);
	drawing_color(rgb(0,255,0));
	draw_fcircle(gamepad_boton_size*1.5/2,gamepad_boton_size*1.5/2,gamepad_boton_size*1.5/2-1);
	drawing_color(rgb(255,255,255));
	draw_circle(gamepad_boton_size*1.5/2,gamepad_boton_size*1.5/2,gamepad_boton_size*1.5/2-1);
	
	graph_boton_2=new_map(gamepad_boton_size*1.5,gamepad_boton_size*1.5,16);
	drawing_map(0,graph_boton_2);
	drawing_color(rgb(255,0,0));
	draw_fcircle(gamepad_boton_size*1.5/2,gamepad_boton_size*1.5/2,gamepad_boton_size*1.5/2-1);
	drawing_color(rgb(255,255,255));
	draw_circle(gamepad_boton_size*1.5/2,gamepad_boton_size*1.5/2,gamepad_boton_size*1.5/2-1);
	
	graph_boton_3=new_map(gamepad_boton_size*1.5,gamepad_boton_size*1.5,16);
	drawing_map(0,graph_boton_3);
	drawing_color(rgb(0,0,255));
	draw_fcircle(gamepad_boton_size*1.5/2,gamepad_boton_size*1.5/2,gamepad_boton_size*1.5/2-1);
	drawing_color(rgb(255,255,255));
	draw_circle(gamepad_boton_size*1.5/2,gamepad_boton_size*1.5/2,gamepad_boton_size*1.5/2-1);
	
	graph_boton_arriba=new_map(2*gamepad_boton_size,gamepad_boton_size,16);
	drawing_map(0,graph_boton_arriba);
	drawing_color(rgb(128,128,128));
	i=0;
	from j=0 to gamepad_boton_size/2;
		draw_line(gamepad_boton_size-i,j,gamepad_boton_size+i,j);
		i+=2;
	end
	from j=gamepad_boton_size/2 to gamepad_boton_size;
		draw_line(gamepad_boton_size-i,j,gamepad_boton_size+i,j);
		i-=1;
	end
	drawing_color(rgb(255,255,255));
	draw_line(0,gamepad_boton_size/2,gamepad_boton_size,0);
	draw_line(gamepad_boton_size,0,2*gamepad_boton_size,gamepad_boton_size/2);
	draw_line(2*gamepad_boton_size,gamepad_boton_size/2,6*gamepad_boton_size/4,gamepad_boton_size);
	draw_line(6*gamepad_boton_size/4,gamepad_boton_size,2*gamepad_boton_size/4,gamepad_boton_size);
	draw_line(2*gamepad_boton_size/4,gamepad_boton_size,0,gamepad_boton_size/2);
	
	graph_boton_abajo=new_map(2*gamepad_boton_size,gamepad_boton_size,16);
	drawing_map(0,graph_boton_abajo);
	drawing_color(rgb(128,128,128));
	i=0;
	from j=0 to gamepad_boton_size/2;
		draw_line(gamepad_boton_size-i,gamepad_boton_size-j,gamepad_boton_size+i,gamepad_boton_size-j);
		i+=2;
	end
	from j=gamepad_boton_size/2 to gamepad_boton_size;
		draw_line(gamepad_boton_size-i,gamepad_boton_size-j,gamepad_boton_size+i,gamepad_boton_size-j);
		i-=1;
	end
	drawing_color(rgb(255,255,255));
	draw_line(0,gamepad_boton_size/2,gamepad_boton_size,gamepad_boton_size);
	draw_line(gamepad_boton_size,gamepad_boton_size,2*gamepad_boton_size,gamepad_boton_size/2);
	draw_line(2*gamepad_boton_size,gamepad_boton_size/2,6*gamepad_boton_size/4,0);
	draw_line(6*gamepad_boton_size/4,0,2*gamepad_boton_size/4,0);
	draw_line(2*gamepad_boton_size/4,0,0,gamepad_boton_size/2);
	
	graph_boton_izquierda=new_map(gamepad_boton_size,2*gamepad_boton_size,16);
	drawing_map(0,graph_boton_izquierda);
	drawing_color(rgb(128,128,128));
	i=0;
	from j=0 to gamepad_boton_size/2;
		draw_line(j,gamepad_boton_size-i,j,gamepad_boton_size+i);
		i+=2;
	end
	from j=gamepad_boton_size/2 to gamepad_boton_size;
		draw_line(j,gamepad_boton_size-i,j,gamepad_boton_size+i);
		i-=1;
	end
	drawing_color(rgb(255,255,255));
	draw_line(gamepad_boton_size/2,0,0,gamepad_boton_size);
	draw_line(0,gamepad_boton_size,gamepad_boton_size/2,2*gamepad_boton_size);
	draw_line(gamepad_boton_size/2,2*gamepad_boton_size,gamepad_boton_size,6*gamepad_boton_size/4);
	draw_line(gamepad_boton_size,6*gamepad_boton_size/4,gamepad_boton_size,2*gamepad_boton_size/4);
	draw_line(gamepad_boton_size,2*gamepad_boton_size/4,gamepad_boton_size/2,0);
	
	graph_boton_derecha=new_map(gamepad_boton_size,2*gamepad_boton_size,16);
	drawing_map(0,graph_boton_derecha);
	drawing_color(rgb(128,128,128));
	i=0;
	from j=0 to gamepad_boton_size/2;
		draw_line(gamepad_boton_size-j,gamepad_boton_size-i,gamepad_boton_size-j,gamepad_boton_size+i);
		i+=2;
	end
	from j=gamepad_boton_size/2 to gamepad_boton_size;
		draw_line(gamepad_boton_size-j,gamepad_boton_size-i,gamepad_boton_size-j,gamepad_boton_size+i);
		i-=1;
	end
	drawing_color(rgb(255,255,255));
	draw_line(gamepad_boton_size/2,0,gamepad_boton_size,gamepad_boton_size);
	draw_line(gamepad_boton_size,gamepad_boton_size,gamepad_boton_size/2,2*gamepad_boton_size);
	draw_line(gamepad_boton_size/2,2*gamepad_boton_size,0,6*gamepad_boton_size/4);
	draw_line(0,6*gamepad_boton_size/4,0,2*gamepad_boton_size/4);
	draw_line(0,2*gamepad_boton_size/4,gamepad_boton_size/2,0);
End

//-----------------------------------------------------------------------
// proceso text_input muestra el teclado
//-----------------------------------------------------------------------

Process text_input(x1,y1,longitud_max,texto_j1,texto_j2,texto_j3,texto_j4);
private
	posicion;
	texto_jugadores[5];
Begin
	graph=new_map(((len(cadena)+2)/filas_cadena)*size_fuente,filas_cadena*size_fuente,bpp);
	drawing_map(0,graph);
	drawing_color(rgb(0,255,0));
	draw_rect(0,0,(((len(cadena)+2)/filas_cadena)*size_fuente)-1,(filas_cadena*size_fuente)-1);
	
	from jugador=1 to jugadores;
		texto_introducido[jugador]="";
	end
	
	texto_jugadores[1]=texto_j1;
	texto_jugadores[2]=texto_j2;
	texto_jugadores[3]=texto_j3;
	texto_jugadores[4]=texto_j4;
	
	from jugador=1 to jugadores;
		if(texto_jugadores[jugador])
			write(fuente_teclado,(ancho_pantalla/2)-(longitud_max*15),20*jugador+100,5,"jugador "+jugador);
			write_var(fuente_teclado,ancho_pantalla/2,20*jugador+100,5,texto_introducido[jugador]);
			cursor(x1,y1,jugador,longitud_max);
		end
	end
	
	for(y=(size_fuente/2);y<((filas_cadena*size_fuente)+(size_fuente/2));y+=size_fuente)
		for(x=(size_fuente/2);x<((((len(cadena)+2)/filas_cadena)*size_fuente)+(size_fuente/2));x+=size_fuente)
			if(posicion==len(cadena)+1)
				write(fuente_teclado,x+x1-(((len(cadena)+2)/filas_cadena)*size_fuente/2),y+y1-(filas_cadena*size_fuente/2),4,"Fin");
			elseif(posicion==len(cadena))
				write(fuente_teclado,x+x1-((((len(cadena)+2)/filas_cadena))*size_fuente/2),y+y1-(filas_cadena*size_fuente/2),4,"Bor");
			else
				write(fuente_teclado,x+x1-(((len(cadena)+2)/filas_cadena)*size_fuente/2),y+y1-(filas_cadena*size_fuente/2),4,cadena[posicion]);
			end
			posicion++;
			if(posicion>len(cadena)+1) break; end
		end
		if(posicion>len(cadena)+1) break; end
	end
	
	x=x1;
	y=y1;
	
	loop
		if(!exists(TYPE cursor)) break; end
		frame;
	end
	unload_map(0,graph);
End

//-----------------------------------------------------------------------
// proceso cursor para introducir texto
//-----------------------------------------------------------------------

process cursor(x1,y1,jugador,longitud_max);
private
	posicion;
	letra_selecionada;
	pulsando;
begin

	graph=new_map(size_fuente,size_fuente,bpp);
	drawing_map(0,graph);
	switch(jugador)
		case 1:
			drawing_color(rgb(255,0,0));
			draw_box(0,0,(size_fuente*1/3),(size_fuente*1/3));
			set_text_color(rgb(255,255,255));
			map_put (0,graph,write_in_map(0,"1",4),(size_fuente*1/6),(size_fuente*1/6));
		end
		case 2:
			drawing_color(rgb(0,255,0));
			draw_box((size_fuente*2/3),0,size_fuente-1,(size_fuente*1/3));
			set_text_color(rgb(255,255,255));
			map_put (0,graph,write_in_map(0,"2",4),(size_fuente*5/6),(size_fuente*1/6));
		end
		case 3:
			drawing_color(rgb(255,255,0));
			draw_box(0,(size_fuente*2/3),(size_fuente*1/3),size_fuente-1);
			set_text_color(rgb(255,255,255));
			map_put (0,graph,write_in_map(0,"3",4),(size_fuente*1/6),(size_fuente*5/6));
		end
		case 4:
			drawing_color(rgb(0,0,255));
			draw_box((size_fuente*2/3),(size_fuente*2/3),size_fuente-1,size_fuente-1);
			set_text_color(rgb(255,255,255));
			map_put (0,graph,write_in_map(0,"4",4),(size_fuente*5/6),(size_fuente*5/6));
		end
	end
	draw_line(0,0,0,(size_fuente*1/3)-1);
	draw_line(0,0,(size_fuente*1/3)-1,0);
	
	draw_line((size_fuente*2/3)-1,0,size_fuente-1,0);
	draw_line(size_fuente-1,0,size_fuente-1,(size_fuente*1/3)-1);
	
	draw_line(0,size_fuente*2/3,0,size_fuente-1);
	draw_line(0,size_fuente-1,(size_fuente*1/3)-1,size_fuente-1);
	
	draw_line(size_fuente-1,size_fuente-1,size_fuente-1,(size_fuente*2/3)-1);
	draw_line((size_fuente*2/3)-1,size_fuente-1,size_fuente-1,size_fuente-1);
	
//	mouse.graph=new_map(size_fuente/2,size_fuente/2,32);
//	drawing_map(0,mouse.graph);
//	draw_line(size_fuente/4,0,size_fuente/4,size_fuente/2);
//	draw_line(0,size_fuente/4,size_fuente/2,size_fuente/4);
	
	controlador(jugador);
	loop
		
		if(p[jugador].botones[b_derecha])
			posicion++;
		end
		if(p[jugador].botones[b_izquierda])
			posicion--;
		end
		if(p[jugador].botones[b_arriba])
			posicion-=((len(cadena)+2)/filas_cadena);
		end
		if(p[jugador].botones[b_abajo])
			posicion+=((len(cadena)+2)/filas_cadena);
		end
		if(p[jugador].botones[b_1])
			if(posicion==len(cadena)+1) //Fin
				break;
			elseif(posicion==len(cadena)) //Borra
				texto_introducido[jugador]=substr(texto_introducido[jugador],0,len(texto_introducido[jugador])-1);
			else //Escribe
				if(len(texto_introducido[jugador])<longitud_max)
					texto_introducido[jugador]+=""+cadena[posicion];
				end
			end
		end
		if(p[jugador].botones[b_2])
			texto_introducido[jugador]=substr(texto_introducido[jugador],0,len(texto_introducido[jugador])-1);
		end
		
		if(mouse.y < father.y+(filas_cadena*size_fuente/2) and mouse.y > father.y-(filas_cadena*size_fuente/2) and mouse.x < father.x+(((len(cadena)+2)/filas_cadena)*size_fuente/2) and mouse.x > father.x-(((len(cadena)+2)/filas_cadena)*size_fuente/2))
			posicion=((( (mouse.y-father.y+(filas_cadena*size_fuente/2))/size_fuente)*((len(cadena)+2)/filas_cadena)) + ((mouse.x-father.x+(((len(cadena)+2)/filas_cadena)*size_fuente/2))/(size_fuente) ));
		end
		
		if(mouse.left and pulsando==0) //Modo táctil
			if(posicion==len(cadena)+1) //Fin
				break;
			elseif(posicion==len(cadena)) //Borra
				texto_introducido[jugador]=substr(texto_introducido[jugador],0,len(texto_introducido[jugador])-1);
			else //Escribe
				if(len(texto_introducido[jugador])<longitud_max)
					texto_introducido[jugador]+=""+cadena[posicion];
				end
			end
			pulsando=1;
		end
		if(pulsando==1 and !mouse.left) pulsando=0; end
		
		if(posicion<0) posicion+=len(cadena)+2; end
		if(posicion>len(cadena)+1) posicion-=len(cadena)+2; end
		
		x=x1-(((len(cadena)+2)/filas_cadena)*size_fuente/2)+(size_fuente/2)+(size_fuente*(posicion mod ((len(cadena)+2)/filas_cadena)));
		y=y1-(filas_cadena*size_fuente/2)+(size_fuente/2)+(size_fuente*(posicion/((len(cadena)+2)/filas_cadena)));
		
		if(posicion==len(cadena)+1)
			letra_selecionada=write(fuente_teclado,(ancho_pantalla/2)+1,(20*jugador)+100,3,"Fin");
		else
			if(len(texto_introducido[jugador])<longitud_max)
				letra_selecionada=write(fuente_teclado,(ancho_pantalla/2)+1,(20*jugador)+100,3,cadena[posicion]);
			end
		end
		while(p[jugador].botones[b_derecha] or p[jugador].botones[b_izquierda] or p[jugador].botones[b_arriba] or p[jugador].botones[b_abajo] or p[jugador].botones[b_1] or p[jugador].botones[b_2])
			frame;
		end
		frame;
		delete_text(letra_selecionada);
	end
	unload_map(0,graph);
end


