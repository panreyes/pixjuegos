Process muneco2(jugador);
Private
	pulsando_salto;
	pulsando_disparo;
	id_col_bola;
	choca_enemigo;
	sobre_enemigo;
	j; j2;
	ipendiente;
	sepuedebajar;
Begin
	if(tipo_nivel==1) ctype=c_scroll; end	
	controlador(jugador);
	// anim aparicion
	z=-1;
	file=fpg_pux;

	graph=1;
	ancho=graphic_info(file,graph,g_width);
	alto=graphic_info(file,graph,g_height);
	x=muneco2x; y=muneco2y;
	p[jugador].identificador=id;
	p[jugador].invencibilidad=-60;

	repeat
		while(accion==atrapado) graph=2; frame; end
		while(!ready) frame; end
	if(accion!=chilena)
		//disparo
		if(pulsando_disparo==0 and accion!=disparando)
			if(botones.p[jugador][4])
				pulsando_disparo=1;
				accion=disparando;
				anim=0;
				disparo2(); //CUIDADO!
			end
		end
		if(!botones.p[jugador][4]) pulsando_disparo=0; end

		//colisiones
	if(accion!=muere and accion!=atrapado)
		if(id_col_bola=collision(type enemigo))
			if(id_col_bola.accion!=atrapado and p[jugador].invencibilidad==0 and id_col_bola.accion!=muere)
				break;
			end
		end
		if(id_col_bola=collision(type disparo))
			if(p[jugador].invencibilidad==0 and id_col_bola.accion!=muere)
				id_col_bola.accion=muere;
				break;
			end
		end
		if(id_col_bola=collision(type jefe))
			if(p[jugador].invencibilidad==0 and id_col_bola.accion!=muere)
				id_col_bola.accion=muere;
				break;
			end
		end
		if(id_col_bola=collision(type enemigo_jefe))
			if(p[jugador].invencibilidad==0 and id_col_bola.accion!=muere)
				id_col_bola.accion=muere;
				break;
			end
		end
	end

		//izquierda
		if(botones.p[jugador][0])
			if(accion!=disparando) accion=anda; end
			flags=1;
			if(!p[jugador].velocidad)
				if(saltando==0) incx-=4; else incx-=3; end
			else
				if(saltando==0) incx-=10; else incx-=8; end
			end
		end

		//derecha
		if(botones.p[jugador][1])
			if(accion!=disparando) accion=anda; end
			flags=0;
			if(!p[jugador].velocidad)
				if(saltando==0) incx+=4; else incx+=3; end
			else
				if(saltando==0) incx+=10; else incx+=8; end
			end
		end

		if(!botones.p[jugador][0] and !botones.p[jugador][1] and accion==anda)
			accion=0;
			graph=1;
		end

		//salto
		if(saltando==0 and pulsando_salto==0)
			if(botones.p[jugador][5] and !botones.p[jugador][3])
				sonido(7);
				y-=2;
				grav=-16*(gravedad)/3;
				saltando=1;
				pulsando_salto=1;
			elseif(botones.p[jugador][5] and botones.p[jugador][3])
				sepuedebajar=0;
				from j2=y+(alto/2)+1 to 480; if(map_get_pixel(0,masknivel,x,j2)==color_colision or map_get_pixel(0,masknivel,x,j2)==color_pendiente) sepuedebajar=1; end end
				if(sepuedebajar)
					y+=1;
				end
			end
		end

		if(!botones.p[jugador][5]) pulsando_salto=0; end
	end
		incy=grav;
		if(grav<0) grav++; end
		//pendientes
		if(map_get_pixel(0,masknivel,x,y+(alto/2)+1)==color_pendiente)
			y++;
		end
		if(map_get_pixel(0,masknivel,x,y+(alto/2)-1)==color_pendiente)
			y--;
		end		

		if((map_get_pixel(0,masknivel,x,y+(alto/2))==color_colision or map_get_pixel(0,masknivel,x,y+(alto/2))==color_pendiente) and grav=>0)
			grav=0;
			saltando=0;
		else
			grav++;
			saltando=1;
		end

		//movimiento
		if(incx!=0)

			while(incx>0 and map_get_pixel(0,masknivel,x+(ancho/3)+1,y)!=color_colision)
				incx--;
				x++;

				if(map_get_pixel(0,masknivel,x,y+(alto/2)+1)==color_pendiente)
					y++;
				end
				if(map_get_pixel(0,masknivel,x,y+(alto/2)-1)==color_pendiente)
					y--;
				end		
			end
			while(incx<0 and map_get_pixel(0,masknivel,x-(ancho/3)-1,y)!=color_colision)
				incx++;
				x--;
				if(map_get_pixel(0,masknivel,x,y+(alto/2)+1)==color_pendiente)
					y++;
				end
				if(map_get_pixel(0,masknivel,x,y+(alto/2)-1)==color_pendiente)
					y--;
				end
			end
			incx=0;
		end
		if(incy!=0)
			while(incy>0 and (map_get_pixel(0,masknivel,x,y+(alto/2))!=color_colision and map_get_pixel(0,masknivel,x,y+(alto/2))!=color_pendiente))
				incy--;
				y++;
			end
			while(incy<0)
				incy++;
				y--;
			end
			incy=0;
		end
		if(y>alto_nivel-1+alto/2) y=-alto/2; end
		if(y<-alto/2) y=480-1+alto/2; end
		if(x>ancho_nivel-31) x=ancho_nivel-31; end
		if(x<30) x=30; end
		if(p[jugador].invencibilidad<0) flags+=4; end


	// inicio dibujines
		if(accion!=chilena)
			if(p[jugador].velocidad==0)
				if(saltando and accion!=disparando)
					graph=2;
				elseif(accion==disparando)
					if(p[jugador].tocho)
						if(anim<3)
							graph=7; anim++;
						elseif(anim<6)
							graph=8; anim++;
						else
							graph=1;
							accion=0;
							anim=0;
						end
					else
						if(anim<8)
							graph=7; anim++;
						elseif(anim<20)
							graph=8; anim++;
						else
							graph=1;
							accion=0;
							anim=0;
						end
					end
				elseif(accion==anda)
					if(anim<5)
						graph=3; anim++;
					elseif(anim<10)
						graph=4; anim++;
					elseif(anim<15)
						graph=5; anim++;
					elseif(anim<20)
						graph=6; anim++;
					else
						graph=3;
						anim=0;
					end
				else
					graph=1;
					accion=0;
					anim=0;
				end
			else
				if(saltando and accion!=disparando)
					graph=2;
					if(flags==0) angle+=50000; else angle-=50000; end
				elseif(accion==disparando)
					if(p[jugador].tocho)
						if(anim<3)
							graph=7; anim++;
						elseif(anim<6)
							graph=8; anim++;
						else
							graph=1;
							accion=0;
							anim=0;
						end
					else
						if(anim<8)
							graph=7; anim++;
						elseif(anim<20)
							graph=8; anim++;
						else
							graph=1;
							accion=0;
							anim=0;
						end
					end
				else
					if(anim<1)
						graph=3; anim++;
					elseif(anim<2)
						graph=4; anim++;
					elseif(anim<3)
						graph=5; anim++;
					elseif(anim<4)
						graph=6; anim++;
					else
						graph=3;
						anim=0;
					end

				end
			end
		else
			if(graph!=9)
				graph=9;
				anim=0;
			end
			if(anim<10)
				if(flags==0) angle=anim*18000; else angle=-anim*18000; end
				anim++;
			else
				accion=0;
				graph=2;
			end
		end
		// fin dibujines
		if(angle!=0 and saltando==0) angle=0; end
		frame;
		if(p[jugador].velocidad) sombra(); end
		if(p[jugador].invencibilidad<0) p[jugador].invencibilidad++; flags-=4; end
		if(flags<0) flags=0; end
	until(accion==muere);

sonido(10);
	// anim muerte
	from anim=0 to 30;
		y-=2;
		switch(anim)
			case 0..2,6..8:
				graph=10;
			end
			case 3..5,9..11,15..17:
				graph=11;
			end
			case 12..14,18..20:
				graph=12;
			end
		end
		frame;
	end
	from alpha=255 to 0 step -20; size-=5; angle+=70000; frame; end
	p[jugador].velocidad=0;
	p[jugador].lejos=0;
	p[jugador].tocho=0;
	// fin anim muerte
End