global
	elecc;
	elecy;
	pr1;
	pr2;
	pr3;
end

process menu();
private
	tec;
	keytime;
	tec2;
	continuable;
begin
	file=fpg_menu;
	delete_text(all_text);
	let_me_alone();
	reinicia_variables();
	jugador=1;
	controlador(0);
	musica("2");
	elecc=0;
	lista(3);
	logo(2);
	machango();
	from i=0 to 20;
		elep();
	end
	if(file_exists(savegamedir+"partida.dat")); continuable=1; end
	graph=1;
	x=320;
	y=240;
	keytime=10;
	loop
		if(elecc==7)
			elecc=0;
		end
		if(elecc==-1)
			elecc=6;
		end
		if(keytime>0)
			keytime--;
		end
		//elige algo
		if(p[0].botones[4] and keytime==0)
			if(ops.sonido==1)
				sonido(3);
			end
			if(elecc==0)
				let_me_alone();
				back(1);
				elecpersonaje();
				break;
			end
			if(elecc==1)
				let_me_alone();
				fondo_elecpersonaje_continuar();
				cargar_partida();
				break;
			end
			if(elecc==2)
				let_me_alone();
				opcion();
				break;
			end
			if(elecc==3)
				let_me_alone();
				back(20);
				break;
			end
			if(elecc==4)
				let_me_alone();
				back(200);
				break;
			end
			if(elecc==5)
 				if(os_id==0 or os_id==os_wii)
 					shell("http://pixjuegos.com");
 				else
					shell("x-www-browser http://pixjuegos.com &");
 				end
				save(savegamedir+"opciones.dat",ops);
				exit(0);
			end
			if(elecc==6)
				save(savegamedir+"opciones.dat",ops);

				frame(300);
				exit(0);
			end
		end
		if(p[0].botones[3])
			if(tec==0)
				elecc++;
				if(elecc==1 and !continuable)
					elecc++;
				end
				if(ops.sonido==1)
					sonido(2);
				end
				tec=1;
			end
		else
			tec=0;
		end
		if(p[0].botones[2])
			if(tec2==0)
				elecc--;
				if(elecc==1 and !continuable)
					elecc--;
				end
				if(ops.sonido==1)
					sonido(2);
				end
				tec2=1;
			end
		else
			tec2=0;
		end
		frame;
	end
end

process lista(graph);
begin
	file=fpg_menu;
	x=-200;
	y=240;
	z=-10;
	if(graph==3 or graph==30) file=fpg_menu2; end
	loop
		x+=(x-150)/-10;
		frame;
	end
end

process logo(graph);
begin
	file=fpg_menu;
	x=1000;
	y=170;
	z=-10;
	loop
		x+=(x-410)/-10;
		frame;
	end
end

process elep();
private
	ox;
	oy;
begin
	file=fpg_menu;
	graph=rand(10,12);
	z=-5;
	loop
		if(rand(0,10)==1)
			ox=rand(10,150);
			oy=rand(elecy-50,elecy+50);
		end
		x+=(x-ox)/-10;
		y+=(y-oy)/-10;
		frame;
	end
end

process machango();
begin
	file=fpg_menu;
	z=-20;
	graph=50;
	x=20;
	loop
		elecy=y;
		if(elecc==0)
			y=65;
		end
		if(elecc==1)
			y=120;
		end
		if(elecc==2)
			y=180;
		end
		if(elecc==3)
			y=235;
		end
		if(elecc==4)
			y=295;
		end
		if(elecc==5)
			y=355;
		end
		if(elecc==6)
			y=410;
		end
		frame;
	end
end

process back(graph);
private
	keytime;
begin
	file=fpg_menu;
	if(graph==4 or graph==5 or graph==20 or graph==21 or graph==200) file=fpg_menu2; end
	x=320;
	y=240;
	z=50;
	keytime=10;
	jugador=1;
	controlador(0);
	if(njoys=>1) controlador(2); end
	if(njoys=>2) controlador(3); end

	while(p[0].botones[4]) frame; end
	loop
		if(p[0].botones[5])
			sonido(1);
			let_me_alone();
			menu();
			break;
		end
		//ayuda y tal
		if((graph>=20 and graph<=22) or (graph=>200 and graph<=202))
			if(p[0].botones[4] and keytime==0)
				graph++;
				if(graph==22 or graph==202)
					sonido(1);
					let_me_alone();
					menu();
					break;
				end
				keytime=10;
			end
			if(keytime>0)
				keytime--;
			end
		end
		if(graph==201) file=fpg_menu; end
		//ayuda y tal
		frame;
	end
end

process elecpersonaje();
private
	keytime;
	juegan;
begin
	p[1].muneco=0;
	p[2].muneco=0;
	p[3].muneco=0;
	if(posibles_jugadores!=1)
		timer[0]=0;
		pulsa_pa_jugar(1);
		pulsa_pa_jugar(2);
		pulsa_pa_jugar(3);
		write_int(fnt_intro,320,350,4,&i);
		while(timer[0]<500)
			ponimgn(320,280,100,6);
			if(p[1].juega+p[2].juega+p[3].juega==0) timer[0]=0; end
			if(p[1].juega+p[2].juega+p[3].juega=>posibles_jugadores or 
			p[1].juega+p[2].juega+p[3].juega==3) timer[0]=500; end
			i=(500-timer[0])/100;
			frame;
		end
		sonido(3);
		from i=1 to 3; if(p[i].juega) juegan++; end	end
		from i=1 to 3; if(p[i].juega) p[i].vidas=6/juegan; end end
		delete_text(all_text);
	else
		p[1].juega=1;
		p[1].vidas=6;
	end
	textoelecpersonaje();
	elecpersonajes(1);
	frame(1000);
	elecpersonajes(2);
	frame(1000);
	elecpersonajes(3);
	frame(1000);
	elige_personaje(1);
end

Process textoelecpersonaje();
Begin
	graph=7;
	z=-10;
	x=320;
	y=100;
	file=fpg_menu2;
	loop
		frame;
	end
End

process pulsa_pa_jugar(jugador);
private
	id_controlador;
begin
	y=100;
	file=fpg_menu2;
	switch(jugador)
		case 1:
			x=160;
		end
		case 2:
			x=320;
		end
		case 3:
			x=480;
		end
	end
	id_controlador=controlador(jugador);
	p[jugador].juega=0;
	while(p[jugador].botones[4]==0)
		if(timer[0]>500) signal(id_controlador,s_kill); return; end
		frame;
	end
	p[jugador].juega=1;
	signal(id_controlador,s_kill);
	graph=500+jugador;
	z=-10;
	size=84;
	from alpha=60 to 240 step 20; size+=2; frame; end
	while(timer[0]<500) frame; end
	from alpha=240 to 0 step -40; frame; end
end

process elige_personaje(jugador);
private
	personaje_elegido;
	id_controlador;
begin
	if(p[jugador].juega==0)
		if(jugador!=3) 
			elige_personaje(jugador+1); 
			return; 
		else 
			nivel();
		end
	end
	id_controlador=controlador(jugador);
	file=fpg_menu2;
	graph=500+jugador;
	personaje_elegido=jugador;
	while(p[1].muneco==personaje_elegido or p[2].muneco==personaje_elegido or p[3].muneco==personaje_elegido)
		personaje_elegido++;
		if(personaje_elegido==4) personaje_elegido=1; end
	end
	z=-3;
	y=360;
	switch(personaje_elegido)
		case 1:
			x=160;
		end
		case 2:
			x=320;
		end
		case 3:
			x=480;
		end
	end
	size=84;
	from alpha=60 to 220 step 20; size+=2; frame; end
	repeat
		if(p[jugador].botones[0])
			sonido(2);
			while(p[jugador].botones[0]) frame; end
			personaje_elegido--;
			if(personaje_elegido=<0) personaje_elegido=3; end
			while(p[1].muneco==personaje_elegido or p[2].muneco==personaje_elegido or p[3].muneco==personaje_elegido)
				personaje_elegido--;
			end
		end
		if(p[jugador].botones[1])
			sonido(2);
			while(p[jugador].botones[1]) frame; end
			personaje_elegido++;
			if(personaje_elegido==4) personaje_elegido=1; end
			while(p[1].muneco==personaje_elegido or p[2].muneco==personaje_elegido or p[3].muneco==personaje_elegido)
				personaje_elegido++;
			end
		end
		switch(personaje_elegido)
			case 1:
				x=160;
			end
			case 2:
				x=320;
			end
			case 3:
				x=480;
			end
		end
		frame;
	until(p[jugador].botones[4]);
	while(p[jugador].botones[4]) frame; end
	signal(id_controlador,s_kill);
	p[jugador].muneco=personaje_elegido;
	sonido(3);
	from alpha=240 to 0 step -40; y++; frame; end
	if(jugador!=3) 
		elige_personaje(jugador+1); 
	else 
		frame(3000); 
//		let_me_alone(); 
		nivel(); 
	end
end

process elecpersonajes(num_personaje);
begin
	z=-1;
	y=-240;
	file=fpg_menu;
	switch(num_personaje)
		case 1:
			x=160;
		end
		case 2:
			x=320;
		end
		case 3:
			x=480;
		end
	end
	graph=299+num_personaje*5;
	while(y<240)
		y+=grav;
		grav+=2;
		frame;
	end
	y=240;
	repeat
		from graph=295+num_personaje*5 to 297+num_personaje*5; frame(400); end
		graph=298+num_personaje*5;
		frame(400);
	until(p[1].muneco==num_personaje or p[2].muneco==num_personaje or p[3].muneco==num_personaje)
	grav=0;
	graph=299+num_personaje*5;
	while(y<700)
		y+=grav;
		grav+=2;
		frame;
	end
end

process ponimgn(x,y,size,graph);
begin
	file=fpg_menu;
	if(graph==6 or graph==7)
		file=fpg_menu2;
	end
	z=-50;
	frame;
end

process opcion();
private
	tec;
	tec2;
	tecenter;
begin
	file=fpg_menu;
	elecc=0;
	jugador=1;
	controlador(1);
	back(1);
	lista(30);
	logo(105);
	machango();
	from i=0 to 20; elep(); end
	tecenter=1;
	loop
		if(ops.dificultad==0)
			ponimgn(100,300,100,250);
		end
		if(ops.dificultad==1)
			ponimgn(115,300,100,251);
		end
		if(ops.dificultad==2)
			ponimgn(130,300,100,252);
		end
		if(p[1].botones[4] or p[2].botones[4] or p[3].botones[4])
			if(tecenter==0)
				if(elecc==0)
					if(full_screen==0)
						full_screen=1;
						ops.ventana=0;
						set_mode(640,480,bitscolor);
					else
						full_screen=0;
						ops.ventana=1;
						set_mode(640,480,bitscolor);
					end
				end
				if(elecc==1)
					if(ops.sonido==1)
						ops.sonido=0;
					else
						ops.sonido=1;
						sonido(2);
					end
				end
				if(elecc==2)
					if(ops.musica==1)
						ops.musica=0;
						musica("2");
					else
						ops.musica=1;
						musica("2");
					end
				end
				if(elecc==3)
					ops.dificultad++;
					if(ops.dificultad==3)
						ops.dificultad=0;
					end
				end
			end
			tecenter=1;
		else
			tecenter=0;
		end //enter

		if((p[1].botones[3] or p[2].botones[3] or p[3].botones[3]))
			if(tec==0)
				if(ops.sonido==1)
					sonido(2);
				end
				elecc++;
				tec=1;
			end
		else
			tec=0;
		end
		if((p[1].botones[2] or p[2].botones[2] or p[3].botones[2]))
			if(tec2==0)
				if(ops.sonido==1)
					sonido(2);
				end
				elecc--;
				tec2=1;
			end
		else
			tec2=0;
		end
		if(elecc==4)
			elecc=0;
		end
		if(elecc==-1)
			elecc=3;
		end
		frame;
	end
end

process elecpersonaje_ingame(jugador);
private
	keytime;
	juegan;
	matar;
	personaje_elegido;
	id_controlador;
begin
	let_me_alone();
	fondo_elecpersonaje_ingame();
	frame;

	textoelecpersonaje();
	if(p[1].muneco!=1 and p[2].muneco!=1 and p[3].muneco!=1) elecpersonajes(1); frame(1000); end
	if(p[1].muneco!=2 and p[2].muneco!=2 and p[3].muneco!=2) elecpersonajes(2); frame(1000); end
	if(p[1].muneco!=3 and p[2].muneco!=3 and p[3].muneco!=3) elecpersonajes(3); frame(1000); end
	elecpersonajes(p[jugador].muneco);
	p[jugador].muneco=0;
	id_controlador=controlador(jugador);
	file=fpg_menu2;
	graph=500+jugador;
	personaje_elegido=jugador;
	while(p[1].muneco==personaje_elegido or p[2].muneco==personaje_elegido or p[3].muneco==personaje_elegido)
		personaje_elegido++;
		if(personaje_elegido==4) personaje_elegido=1; end
	end
	z=-3;
	y=360;
	switch(personaje_elegido)
		case 1:
			x=160;
		end
		case 2:
			x=320;
		end
		case 3:
			x=480;
		end
	end
	size=84;
	from alpha=60 to 220 step 20; size+=2; frame; end
	repeat
		if(p[jugador].botones[0])
			sonido(2);
			while(p[jugador].botones[0]) frame; end
			personaje_elegido--;
			if(personaje_elegido=<0) personaje_elegido=3; end
			while(p[1].muneco==personaje_elegido or p[2].muneco==personaje_elegido or p[3].muneco==personaje_elegido)
				personaje_elegido--;
			end
		end
		if(p[jugador].botones[1])
			sonido(2);
			while(p[jugador].botones[1]) frame; end
			personaje_elegido++;
			if(personaje_elegido==4) personaje_elegido=1; end
			while(p[1].muneco==personaje_elegido or p[2].muneco==personaje_elegido or p[3].muneco==personaje_elegido)
				personaje_elegido++;
			end
		end
		switch(personaje_elegido)
			case 1:
				x=160;
			end
			case 2:
				x=320;
			end
			case 3:
				x=480;
			end
		end
		frame;
	until(p[jugador].botones[4]);
	while(p[jugador].botones[4]) frame; end
	signal(id_controlador,s_kill);
	p[jugador].muneco=personaje_elegido;
	p[jugador].juega=1;
	p[jugador].puntos=0;
	p[jugador].vidas=3;
	sonido(3);
	from alpha=240 to 0 step -40; y++; frame; end
	frame(3000); let_me_alone(); nivel();
end

process fondo_elecpersonaje_ingame();
Begin
	graph=get_screen();
	fondo_elecpersonaje_ingame2();
	x=320;
	y=240;
	z=0;
	blendop=blendop_new();
	blendop_assign(0,graph,blendop);
	blendop_grayscale(blendop,1);
	alpha=0;
	loop
		if(alpha<255) alpha+=5; end
		frame;
	end
End

process fondo_elecpersonaje_ingame2();
Begin
	graph=get_screen();
	x=320;
	y=240;
	z=1;
	while(father.alpha!=255)
		frame;
	end
	unload_map(0,graph);
End

process fondo_elecpersonaje_continuar();
Begin
//	graph=load_png(savegamedir+"partida.png");
	file=fpg_menu;
	graph=1;
	x=320;
	y=240;
	z=0;
//	blendop=blendop_new();
//	blendop_assign(0,graph,blendop);
//	blendop_grayscale(blendop,1);
	loop
		frame;
	end
End

function elige_lenguaje();
private
	tec;
	tec2;
	tecenter;
	string lenguajelinux;
begin
	file=fpg_menu;
	elecc=0;
	controlador(0);
	lista(8);
	machango();
	from i=0 to 20; elep(); end
	tecenter=1;
	if(os_id==1) 
		lenguajelinux=getenv("LANG");
		lenguajelinux=lenguajelinux[0]+lenguajelinux[1];
		ops.lenguaje=0; lang_suffix="en";
		switch(lenguajelinux)
			case "es": ops.lenguaje=1; lang_suffix="es"; end
			case "it": ops.lenguaje=1; lang_suffix="it"; end
			case "de": ops.lenguaje=1; lang_suffix="de"; end
			case "fr": ops.lenguaje=1; lang_suffix="fr"; end
		end

		fpg_menu2=load_fpg("fpg/menu-"+lang_suffix+".fpg");
		fpg_intro=load_fpg("fpg/intro-"+lang_suffix+".fpg");
		return;
	end
	loop
		if(p[0].botones[4])
			if(tecenter==0)
				if(elecc==0) ops.lenguaje=0; lang_suffix="en"; end
				if(elecc==1) ops.lenguaje=1; lang_suffix="es"; end
				if(elecc==2) ops.lenguaje=2; lang_suffix="it"; end
				if(elecc==3) ops.lenguaje=3; lang_suffix="de"; end
				if(elecc==4) ops.lenguaje=4; lang_suffix="fr"; end
				fpg_menu2=load_fpg("fpg/menu-"+lang_suffix+".fpg");
				fpg_intro=load_fpg("fpg/intro-"+lang_suffix+".fpg");
				tecenter=1;
				while(p[0].botones[4]) frame; end
				return;
			end
		else
			tecenter=0;
		end //enter

		if(p[0].botones[3])
			if(tec==0)
				if(ops.sonido==1)
					sonido(2);
				end
				elecc++;
				tec=1;
			end
		else
			tec=0;
		end
		if(p[0].botones[2])
			if(tec2==0)
				if(ops.sonido==1)
					sonido(2);
				end
				elecc--;
				tec2=1;
			end
		else
			tec2=0;
		end
		if(elecc==5)
			elecc=0;
		end
		if(elecc==-1)
			elecc=4;
		end
		frame;
	end
end
